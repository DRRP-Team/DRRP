#library "inc_shop"
#include "zcommon.acs"
#import "drrplib.acs"

struct ShopItem {
    str name;
    int price;
    int max;
    str item;
    int amount;
    str description;
    str image;
}

enum {
    SHOP_MENU_HEALTH = 0,
    SHOP_MENU_AMMO   = 1,
    SHOP_MENU_STATS  = 2
}

const int SHOP_ID       = -102034;
const int SHOP_ID_MAX   = SHOP_ID + 100;

ShopItem shopMenu[3][6];
int shopMenuItems[3] = { 0, 0, 0 };
str shopMenuNames[3] = { "Health", "Ammo", "Stats" };
int shopCurrentMenu = 0;
int shopCurrentItem = 0;

function void ShopMenuSetItem(
    int menu,
    int pos, str name, int price, int max, str itemClass, int amount,
    str description, str image
) {
    if (pos > 5) throw("[ShopMenuSetItem] Invalid position!");
    ShopItem item;
    item.name = name;
    item.price = price;
    item.max = max;
    item.item = itemClass;
    item.amount = amount;
    item.description = description;
    item.image = image;
    ShopMenu[menu][pos] = item;
}

function void ShopMenuSetItemEmpty(int menu, int pos) {
    if (pos > 5) throw("[ShopMenuSetItem] Invalid position!");
    ShopItem item;
    ShopMenu[menu][pos] = item;
}

function void ShopMenuInitialize(int id) {
    switch (id) {
        default:
            ShopMenuSetItem(SHOP_MENU_HEALTH, 0, "Small medikit", 4, 99, "SmallMedikit", 1, "DRRP_SHOP_SMMED", "SHMED1");
            ShopMenuSetItem(SHOP_MENU_HEALTH, 1, "Large medikit", 15, 99, "LargeMedikit", 1, "DRRP_SHOP_LGMED", "SHMED2");
            ShopMenuSetItem(SHOP_MENU_HEALTH, 2, "Soul sphere", 30, 99, "SoulSphere", 1, "DRRP_SHOP_SOULSPHERE", "SHSOUL");
            ShopMenuSetItem(SHOP_MENU_HEALTH, 3, "Berserk", 40, 99, "DRRPBerserkItem", 1, "DRRP_SHOP_BERSERK", "SHBERS");
            ShopMenuSetItemEmpty(SHOP_MENU_HEALTH, 4);
            shopMenuItems[SHOP_MENU_HEALTH] = 4;

            ShopMenuSetItem(SHOP_MENU_AMMO, 0, "Halon canisters", 5, 999, "DRRPShopBalloon", 10, "DRRP_SHOP_HALONS", "SHBALL");
            ShopMenuSetItem(SHOP_MENU_AMMO, 1, "Bullets", 5, 999, "DRRPShopClipBox", 10, "DRRP_SHOP_BULLETS", "SHCLIP");
            ShopMenuSetItem(SHOP_MENU_AMMO, 2, "Shells", 10, 99, "DRRPShopShellBox", 10, "DRRP_SHOP_SHELLS", "SHSHEL");
            ShopMenuSetItem(SHOP_MENU_AMMO, 3, "Rockets", 10, 99, "DRRPShopRocketBox", 4, "DRRP_SHOP_ROCKETS", "SHROCK");
            ShopMenuSetItem(SHOP_MENU_AMMO, 4, "Cells", 15, 999, "DRRPShopCellPack", 10, "DRRP_SHOP_CELLS", "SHCELL");
            shopMenuItems[SHOP_MENU_AMMO] = 5;

            ShopMenuSetItem(SHOP_MENU_STATS, 0, "+1 Accuracy", 8, 99, "DRRPShopPlayerAccuracy", 1, "DRRP_SHOP_ACCURACY", "");
            ShopMenuSetItem(SHOP_MENU_STATS, 1, "+1 Agility", 8, 99, "DRRPShopPlayerAgility", 1, "DRRP_SHOP_AGILITY", "");
            ShopMenuSetItem(SHOP_MENU_STATS, 2, "+1 Strength", 10, 99, "DRRPShopPlayerStrength", 1, "DRRP_SHOP_STRENGTH", "");
            ShopMenuSetItem(SHOP_MENU_STATS, 3, "+1 Defense", 10, 99, "DRRPShopPlayerDefense", 1, "DRRP_SHOP_DEFENSE", "");
            ShopMenuSetItemEmpty(SHOP_MENU_STATS, 4);
            shopMenuItems[SHOP_MENU_STATS] = 4;
    }
}

function void ClearImage(int id) {
    HudMessage(s:""; HUDMSG_PLAIN, id, CR_WHITE, 0 + HUD_LEFT, 0 + HUD_TOP, 0, 1);
}

function void DrawImage(str image, int id, int x, int y) {
    if (!StrLen(image)) {
        ClearImage(id);
        return;
    }
    SetHudSize(640, 480);
    SetFont(image);
    HudMessage(s:"A"; HUDMSG_PLAIN, id, CR_WHITE, toFixed(x) + HUD_LEFT, toFixed(y) + HUD_TOP, 0, 1);
}

function void DrawImageCenter(str image, int id, int x, int y) {
    if (!StrLen(image)) {
        ClearImage(id);
        return;
    }
    SetHudSize(640, 480);
    SetFont(image);
    HudMessage(s:"A"; HUDMSG_PLAIN, id, CR_WHITE, toFixed(x) + HUD_CENTER, toFixed(y) + HUD_CENTER, 0, 1);
}

function void DrawImageCenterScale(str image, int id, int x, int y) {
    if (!StrLen(image)) {
        ClearImage(id);
        return;
    }
    SetFont(image);
    HudMessage(s:"A"; HUDMSG_PLAIN, id, CR_WHITE, toFixed(x) + HUD_CENTER, toFixed(y) + HUD_CENTER, 0, 1);
}

function void DrawTextRight(str text, int id, int x, int y, int color) {
    SetHudSize(640, 480);
    SetFont("defsmallfont2");
    HudMessage(s:text; HUDMSG_PLAIN, id, color, toFixed(x) + HUD_RIGHT, toFixed(y) + HUD_TOP, 0, 1);
}

function void DrawTextCenter(str text, int id, int x, int y, int color) {
    SetHudSize(640, 480);
    SetFont("defsmallfont2");
    HudMessage(s:text; HUDMSG_PLAIN, id, color, toFixed(x) + HUD_CENTER, toFixed(y) + HUD_TOP, 0, 1);
}

function void DrawTextLeft(str text, int id, int x, int y, int color) {
    SetHudSize(640, 480);
    SetFont("defsmallfont2");
    HudMessage(s:text; HUDMSG_PLAIN, id, color, toFixed(x) + HUD_LEFT, toFixed(y) + HUD_TOP, 0, 1);
}

function void ShopDrawWindowCredits(void) {
    DrawImage("SHCRED", SHOP_ID + 1, 530, 22);
    DrawTextRight("999", SHOP_ID + 22, 520 + 44 + 60, 8 + 5 + 10, CR_YELLOW);
}

function str ShopInvAmText(str classname) {
    return StrParam(i:CheckInventory(classname));
}

function void ShopDrawWindowInventory(void) {
    int tmpHeight = 18 + 8;
    DrawImage("SHBALL", SHOP_ID + 2, 21, 26);
    DrawTextRight(ShopInvAmText("Balloon"), SHOP_ID + 12, 47 + 31, tmpHeight, CR_WHITE);
    DrawImage("SHCLIP", SHOP_ID + 3, 101, 26);
    DrawTextRight(ShopInvAmText("Clip"), SHOP_ID + 13, 125 + 31, tmpHeight, CR_WHITE);
    DrawImage("SHSHEL", SHOP_ID + 4, 175, 27);
    DrawTextRight(ShopInvAmText("DRRPShell"), SHOP_ID + 14, 195 + 31, tmpHeight, CR_WHITE);
    DrawImage("SHROCK", SHOP_ID + 5, 252, 19);
    DrawTextRight(ShopInvAmText("RocketAmmo"), SHOP_ID + 15, 270 + 31, tmpHeight, CR_WHITE);
    DrawImage("SHCELL", SHOP_ID + 6, 321, 25);
    DrawTextRight(ShopInvAmText("Cell"), SHOP_ID + 16, 343 + 31, tmpHeight, CR_WHITE);

    tmpHeight = 58 + 8;
    DrawImage("SHMED1", SHOP_ID + 7, 18, 61);
    DrawTextRight(ShopInvAmText("SmallMedikit"), SHOP_ID + 17, 47 + 31, tmpHeight, CR_WHITE);
    DrawImage("SHMED2", SHOP_ID + 8, 93, 61);
    DrawTextRight(ShopInvAmText("LargeMedikit"), SHOP_ID + 18, 125 + 31, tmpHeight, CR_WHITE);
    DrawImage("SHSOUL", SHOP_ID + 9, 170, 58);
    DrawTextRight(ShopInvAmText("InventorySoulSphere"), SHOP_ID + 19, 195 + 31, tmpHeight, CR_WHITE);
    DrawImage("SHBERS", SHOP_ID + 10, 246, 58);
    DrawTextRight(ShopInvAmText("DRRPBerserkItem"), SHOP_ID + 20, 270 + 31, tmpHeight, CR_WHITE);
    if (CheckInventory("DogCollar")) {
        DrawImage("SHDOGC", SHOP_ID + 11, 320, 55);
        DrawTextRight(ShopInvAmText("DogCollar"), SHOP_ID + 21, 343 + 31, tmpHeight, CR_WHITE);
    }
}

function void ShopDrawWindowStatistics(void) {
    DrawTextCenter("Statistics", SHOP_ID + 23, 75, 276, CR_WHITE);

    int height = 305;
    const int height_inc = 30;
    const int name_offset = 15;
    const int value_offset = 92 + 40;

    DrawTextLeft("Strength:", SHOP_ID + 24, name_offset, height, CR_WHITE);
    DrawTextRight("99", SHOP_ID + 28, value_offset, height, CR_WHITE);
    height += height_inc;
    DrawTextLeft("Defense:", SHOP_ID + 25, name_offset, height, CR_WHITE);
    DrawTextRight("99", SHOP_ID + 29, value_offset, height, CR_WHITE);
    height += height_inc;
    DrawTextLeft("Accuracy:", SHOP_ID + 26, name_offset, height, CR_WHITE);
    DrawTextRight("99", SHOP_ID + 30, value_offset, height, CR_WHITE);
    height += height_inc;
    DrawTextLeft("Agility:", SHOP_ID + 27, name_offset, height, CR_WHITE);
    DrawTextRight("99", SHOP_ID + 31, value_offset, height, CR_WHITE);

    height += 50;
    DrawTextLeft("Level:", SHOP_ID + 32, name_offset, height, CR_WHITE);
    DrawTextRight("99", SHOP_ID + 33, value_offset, height, CR_WHITE);
}

function str ShopPrevMenuName(void) {
    if (shopCurrentMenu > 0) return shopMenuNames[shopCurrentMenu - 1];
    return shopMenuNames[2];
}

function str ShopNextMenuName(void) {
    return shopMenuNames[(shopCurrentMenu + 1) % 3];
}

function void ShopDrawWindowMain(void) {
    DrawTextCenter(StrParam(s:"Buy: ", s:shopMenuNames[shopCurrentMenu]),
        SHOP_ID + 34, 270, 276, CR_WHITE);

    DrawTextLeft(ShopPrevMenuName(), SHOP_ID + 35, 195, 445, CR_WHITE);
    DrawTextRight(ShopNextMenuName(), SHOP_ID + 36, 345, 445, CR_WHITE);

    const int MAIN_ID = SHOP_ID + 40;
    const int height = 300;
    const int height_inc = 26;

    for (int i = 0; i < 5; i++) {
        ShopItem item = shopMenu[shopCurrentMenu][i];
        if (i < shopMenuItems[shopCurrentMenu])
            DrawTextLeft(
                StrParam(s:item.name, s:" (", i:item.price, s:" UAC)"),
                MAIN_ID + i,
                165, height + height_inc * i,
                CR_WHITE
            );
        else
            ClearImage(MAIN_ID + i);

        if (shopCurrentItem == i)
            DrawImage("SHOPSEL", MAIN_ID + 6, 161, 290 + height_inc * i);
    }
}

function void ShopDrawWindowInfo(void) {
    const int SUB_ID = SHOP_ID + 50;
    ShopItem item = shopMenu[shopCurrentMenu][shopCurrentItem];

    /* DrawTextCenter(item.name, SUB_ID + 1, 510, 280, CR_WHITE); */
    DrawTextRight(StrParam(i:item.price, s:" UAC"), SUB_ID + 4, 620, 280, CR_YELLOW);
    SetHudSize(150, 150);
    DrawImageCenterScale(item.image, SUB_ID + 5, 120, 98);
    DrawTextRight(StrParam(s:"x", i:item.amount), SUB_ID + 3, 600, 315, CR_WHITE);
    DrawTextCenter("Press Enter to buy", SUB_ID + 2, 510, 340, CR_YELLOW);
    /* TODO: SetHudWrapWidth */
    DrawTextLeft(item.description, SUB_ID, 412, 366, CR_WHITE);
}

function void ShopDrawWindow(void) {
    DrawImage("SHOP", SHOP_ID_MAX - 1, 0, 0);
    ShopDrawWindowCredits();
    ShopDrawWindowInventory();
    ShopDrawWindowStatistics();
    ShopDrawWindowMain();
    ShopDrawWindowInfo();
}

function void ShopHideWindow(void) {
    for (int i = SHOP_ID; i < SHOP_ID_MAX; i++) ClearImage(i);
}

Script "ShopOpen" (int id) {
    pauseGame();
    useExtendedInput(true);
    ShopMenuInitialize(id);
    LocalAmbientSound("menu/activate", 100);

    int btn = -1;

    while (true) {
        ShopDrawWindow();
        btn = onInput();

        if (btn == INPUT_ESC) break;

        if (btn == INPUT_BOTTOM && shopCurrentItem + 1 < shopMenuItems[shopCurrentMenu]) {
            shopCurrentItem++;
            LocalAmbientSound("menu/cursor", 100);
        }

        if (btn == INPUT_TOP && shopCurrentItem - 1 >= 0) {
            shopCurrentItem--;
            LocalAmbientSound("menu/cursor", 100);
        }

        if (btn == INPUT_LEFT) {
            shopCurrentMenu--;
            if (shopCurrentMenu < 0) {
                shopCurrentMenu = 2;
            }
            shopCurrentItem = 0;
            LocalAmbientSound("menu/cursor", 100);
        }

        if (btn == INPUT_RIGHT) {
            shopCurrentMenu++;
            if (shopCurrentMenu > 2) {
                shopCurrentMenu = 0;
            }
            shopCurrentItem = 0;
            LocalAmbientSound("menu/cursor", 100);
        }

        Delay(1);
    }

    LocalAmbientSound("menu/clear", 100);

    ShopHideWindow();

    useExtendedInput(false);
    resumeGame();
}

