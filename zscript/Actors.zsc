/**
 * Copyright (c) 2017-2022 DRRP-Team
 * 
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

// Items -------------------------------------------------------------------------

class DRRPArmorShard: Inventory replaces ArmorBonus {
    int armorAmountGive;
    Default {
        Radius 20;
        Height 16;
        Tag "$DRRP_I_ARMORSHARD";
        Inventory.PickupMessage "$DRRP_I_GOT_ARMORSHARD";
        +COUNTITEM;
        +INVENTORY.ALWAYSPICKUP;
    }

    override void BeginPlay() {
        armorAmountGive = 4;
    }

    override void Touch( Actor toucher ) {
        if ( !toucher || !( toucher is "DRRPPlayer" ) ) 
            return;

        Inventory armor = DRRPPlayer( toucher ).FindInventory( "DRRPBasicArmor" );
        if ( armor ) 
            if ( DRRPPlayer( toucher ).GetFeature( RPG_MaxArmor ) <= armor.Amount )
                return;

        toucher.A_GiveInventory( "DRRPBasicArmor", armorAmountGive );
        Super.Touch( toucher );
    }

    States {
        Spawn:
            BON2 ABCDCB 6;
            Loop;
    }
}

class DRRPFlakJacket: DRRPArmorShard replaces GreenArmor {
    Default {
        Tag "$DRRP_I_FLAKJACKET";
        Inventory.PickupMessage "$DRRP_I_GOT_FLAKJACKET";
    }

    override void BeginPlay() {
        armorAmountGive = 25;
    }

    States {
        Spawn:
            ARM1 A 6;
            ARM1 B 7 Bright;
            Loop;
    }
}

class DRRPCombatSuit: DRRPArmorShard replaces BlueArmor {
    Default {
        Tag "$DRRP_I_COMBATSUIT";
        Inventory.PickupMessage "$DRRP_I_GOT_COMBATSUIT";
    }

    override void BeginPlay() {
        armorAmountGive = 50;
    }

    States {
        Spawn:
            ARM2 A 6;
            ARM2 B 6 Bright;
            Loop;
    }
}

// Other -------------------------------------------------------------------------

class DRRPActorsHandler: EventHandler {
    const gibDamage = -30;

    enum EBloodFlashTypes {
        BFT_None = 0,
        BFT_Backslash = 1,  // "SPLASH_1"
        BFT_Claws = 2,      // "SPLASH_2"
        BFT_Spot = 3        // "SPLASH_3"
    };

    override void OnRegister() {
        SetOrder( 2174 );
    }

    override void WorldThingDamaged( WorldEvent e ) {
        // For extra gibs:
        if ( e.Thing && e.Thing.bISMONSTER && e.Thing.health < gibDamage ) {

            if ( CVar.FindCVar( "drrp_enable_extended_gibs" ).GetBool() ) {
                Actor gibSpawner = e.Thing.Spawn( "DRRPExtraDamageGibSpawner", e.Thing.pos );
                gibSpawner.master = e.Thing;
            }
        }

        // For player's blood flashes:
        if ( e.Thing is 'DRRPPlayer' ) {
            int flashType = 0;

            if ( e.Inflictor && e.Inflictor.bISMONSTER )
                flashType = ( e.Inflictor is 'LostSoul' )? BFT_Claws : RandomPick( BFT_Backslash, BFT_Claws );
            else if ( !( e.DamageType == 'DRRPFireDmg' ) && !( e.DamageType == 'DRRPFireExtDmg' ) )
                flashType = BFT_Spot;

            if ( flashType )
                e.Thing.ACS_ScriptCall( "BloodFlashVisual", flashType, e.Damage );
        }

        CVar DebugCVar = CVar.GetCVar( "drrp_debug_mode", players[ consoleplayer ] );

        if ( DebugCVar && DebugCVar.GetBool() ) {
            String thing = e.Thing? e.Thing.GetClassName() .. "" : "NULL";
            String damageSource = e.DamageSource? e.DamageSource.GetClassName() .. "" : "NULL";
            String inflictor = e.Inflictor? "=>" .. e.Inflictor.GetClassName()  : "";

            console.printf( TEXTCOLOR_DARKGRAY .. "DRRPActorsHandler::ThingDamaged(): \cX"
                            .. e.Damage .. "\c- to " .. thing .. " (newhp " .. e.thing.health .. ") from " .. damageSource .. inflictor .. TEXTCOLOR_GRAY .. " (type " .. e.DamageType .. ")\c-." );
        }

        Super.WorldThingDamaged( e );
    } // of override void WorldThingDamaged( WorldEvent e ) {
}

class DRRPExtraDamageGibSpawner: Actor {
    int spawnTime;

    Default {
        Radius 0;
        Height 0;
        -SOLID;
        -SHOOTABLE;
    }

    override void PostBeginPlay() {
        if ( !master ) {
            Destroy();
        } else {
            spawnTime = Random( 2, 8 );

            // Will not start a gibs sound if it already included into the actor.
            if ( !master.FindState( "XDeath", true ) )
                S_Sound( "misc/gibbed", CHAN_AUTO );
        }
    } // of override void PostBeginPlay() {

    override void Tick() {
        for ( int i = 0; i < Random( 0, 5 ); i++ ) {
            master.A_SpawnItemEx( "DRRPExtraDamageGib", 
                    FRandom( -4.0, 4.0 ), FRandom( -4.0, 4.0 ), FRandom( 5.0, 10.0 ),
                    FRandom( -6.0, 6.0 ), FRandom( -6.0, 6.0 ), FRandom( 5.0, 10.0 ) );
        }

        if ( !( --spawnTime ) )
            Destroy();
    } // of override void Tick() {
}

class DRRPExtraDamageGib: Actor {
    int rollingDelay, curRollingDelay;

    Default {
        +BOUNCEONWALLS;
        +BOUNCEONFLOORS;
        +BOUNCEONCEILINGS;
        -SOLID;
        -SHOOTABLE;

        Radius 4;
        Height 4;
        Tag "$DRRP_D_GIBS";
    }

    States {
    Spawn:
        TNT1 A 0;
        GIB5 A 0 A_Jump( 255, "Gibs1", "Gibs2", "Gibs3", "Gibs4", "Gibs5", "Gibs6", "Gibs7", "Gibs8" );
    Gibs1: GIB1 "#" 1; goto Death;
    Gibs2: GIB2 "#" 1; goto Death;
    Gibs3: GIB3 "#" 1; goto Death;
    Gibs4: GIB4 "#" 1; goto Death;
    Gibs5: GIB5 "#" 1; goto Death;
    Gibs6: GIB6 "#" 1; goto Death;
    Gibs7: GIB7 "#" 1; goto Death;
    Gibs8: GIB8 "#" 1; goto Death;
    Death:
        "####" "#" 0 {
            curRollingDelay = rollingDelay = Random( 0, 10 );
            scale *= FRandom( 0.1, 0.5 );
            frame = Random( 0, 3 );
        }
        "####" "############################" 2 {
            if ( !( --curRollingDelay ) ) {
                frame = ( frame + 1 ) % 4;
                curRollingDelay = rollingDelay;

                if ( !Random( 0, 1 ) )
                    rollingDelay++;
            }
        }
        "####" "#" 300;
        "####" "##########################" 5 A_FadeOut( 0.02 );
        Wait;
    }
}


class DRRPTeleportFog: TeleportFog replaces TeleportFog {
    int counter;

    override void PostBeginPlay() {
        counter = 30;
        S_Sound( "misc/teleport", CHAN_AUTO );
    }

    void A_DRRP_TeleportationParticles( void ) {
        DRRPActorAdditionalFunctions.SummonDRRPParticle( self, "DRRPTeleportationParticle", 24, 64 );
    }

    States {
        Spawn:
            TNT1 AAAAA 0 A_DRRP_TeleportationParticles();
            TNT1 A 1 A_JumpIf( !counter--, "Null" );
            Loop;
    }
}

class DRRPKronosTeleportFog: TeleportFog {
}

class DRRPLightWrapper: Actor replaces PointLight {
    enum ELightsWrapperTypes {
        LWT_None = 0,
        LWT_NormalLight,
        LWT_AttenuatedLight
    }

    override void PostBeginPlay() {
        DynamicLight dynlight;

        int lightType = CVar.GetCVar( "drrp_lights_wrapper_type" ).GetInt();

        switch ( lightType ) {
            case LWT_NormalLight:
                dynlight = DynamicLight( Spawn( "PointLight", pos ) );
                break;
            case LWT_AttenuatedLight:
                dynlight = DynamicLight( Spawn( "PointLightAttenuated", pos ) );
                break;
            case LWT_None:
            default:
                break;
        } // of switch ( lightType ) {

        if ( dynlight ) {
            dynlight.angle  = angle;
            dynlight.pitch  = pitch;
            dynlight.roll   = roll;
            dynlight.ChangeTID( self.TID );

            for ( int i = 0; i < 5; i++ )
                dynlight.args[ i ] = self.args[ i ];
        }

        Destroy();
    } // of override void PostBeginPlay() {
} // of class DRRPLightWrapper: Actor {

class DRRPLightWrapper1 : DRRPLightWrapper replaces PointLight {}
class DRRPLightWrapper2 : DRRPLightWrapper replaces PointLightAttenuated {}

class DRRPInformatorActor: Actor {
    String levelPrefix;

    Default {
        +SOLID;
        Height 64;
        Radius 32;
    }

    // "LineTrace()" (see "Infobar.zsc") cannot find actors without flag "+SOLID".
    override bool CanCollideWith( Actor other, bool passive ) {
        return false;
    }

    override void PostBeginPlay() {
        int levelnum = ( args[ 1 ] == 0 )? level.levelnum : args[ 1 ];

        switch ( levelnum ) {
            case 1: 
                levelPrefix = "INTRO";
                break;
            case 2: 
                levelPrefix = "HUB";
                break;
            case 10: 
                levelPrefix = "XHUB";
                break;
            case 11: 
                levelPrefix = "REAC";
                break;
            case 12: 
                levelPrefix = "CREDITS";
                break;
            case 99: 
                levelPrefix = "TESTMAP"; // Has it any strings?
                break;
            case 254: 
                levelPrefix = "D_TMAP"; // "[T]itle [map]".
                break;
            case 255: 
                levelPrefix = "B"; // "[B]ase".
                break;
            default:
                if ( levelnum >= 3 && levelnum <= 9 )
                    levelPrefix = "SEC" .. ( levelnum - 2 );
                break;
        }

        //console.printf( "\"" .. informString .. "\" as: " .. "$DRRP_" .. levelPrefix .. "_" .. args[ 0 ] );
        Super.PostBeginPlay();
    }

    String getInformString() { // As number in args[ 0 ].
        return StringTable.Localize( "$DRRP_" .. levelPrefix .. "_" .. args[ 0 ] );
    }

} // of class DRRPInformatorActor: Actor {

class DRRPDamageDoll: DRRPPlayer {
    Default {
        +BUDDHA;
        +DONTTHRUST;
        +NOBLOODDECALS;
        +QUICKTORETALIATE;
        -FRIENDLY;
        //-NOINFIGHTING;
        Health 0x4000;
        Tag "\cGD\cXo\cQl\cHl\c-, \cLnot more\c-.";
    }

    override void Tick() {
        Super.Tick();
        if ( 0x4000 - health )
            console.printf( "(Doll): taked " 
                            .. TEXTCOLOR_YELLOW .. 0x4000 - health .. TEXTCOLOR_NORMAL 
                            .. " HP by " .. ( ( target == NULL )
                                ? "environment"
                                : ( ( target.Player != NULL )
                                    ? "player " .. target.Player.GetUserName()
                                    : target.GetTag() .. " (class " .. target.GetClassName() .. ")"
                                ) 
                            ) .. "." );
        health = 0x4000;
    }
}
