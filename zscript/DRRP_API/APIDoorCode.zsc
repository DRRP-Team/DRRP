/**
 *Copyright (c) 2018-2019 DRRP-Team
 *
 *This software is released under the MIT License.
 *https://opensource.org/licenses/MIT
 */


class DoorCodeInputActor {
    static int DoorCode( Actor activator, string info, string code, int scriptnum ) {
        EventHandler.SendNetworkEvent( "drrp_opendoorinput@@@@" .. info .. "@@@@" .. code, scriptnum );
 
        return 1;
    }
}


// Hack because "InputProcess()" is under UI scope.
struct DoorCodeDynamicInfo {
    String codeStr; // Not an integers because of potential leading zeros (like password "042").
    int curinputpos;

    bool inputActivated, prevInputActive;
}


class DoorCodeInputHandler: EventHandler {
    String windowTooltip;
    String realCodeStr;
    int dialogScriptNo; // The script, that called passcode window
    DoorCodeDynamicInfo door;

    override void OnRegister() {
        SetOrder( 3678 );

        door.codeStr = "";
        door.curinputpos = 0;
        door.inputActivated = false;

        Super.OnRegister();
    }

    void OpenPasscodeWindow(String _windowTooltip, String _realCodeStr, int _dialogScriptNo) {
        door.inputActivated = true;
        door.curinputpos = 0;
        door.codeStr = "";

        windowTooltip = _windowTooltip;
        realCodeStr = _realCodeStr;
        dialogScriptNo = _dialogScriptNo;

        // All the ACS code part based on the level freeze when Menu class is opened...
        ACS_Suspend( dialogScriptNo, 0 );
        //ACS_Terminate( dialogScriptNo, 0 );

        console.printf( TEXTCOLOR_GREEN .. "Your code will be echoed " .. TEXTCOLOR_FIRE .. "here\c-." );
    }

    void OnCorrectPasscode(PlayerPawn player) {
        DRRPUtil.ACS_NamedExecute("clearwindow");
        ACS_Execute( dialogScriptNo, 0 );
        console.printf( "Executed " .. dialogScriptNo );
    }

    void OnIncorrectPasscode(PlayerPawn player) {
        DRRPUtil.ACS_NamedExecute("clearwindow");
        player.A_PlaySound( "access/deny1" .. StringTable.Localize( "$DRRP_D_SOUND_SUFFIX" ) );
        player.A_Print( StringTable.Localize( "$DRRP_D_PASSCODE_WRONG" ) );
        ACS_Terminate( dialogScriptNo, 0 );
    }

    ui void OnRenderWindow(String windowText, String code) {
        console.printf( "Code: \"" .. code .. "\"." );
        // DRRPUtil.SetInternalMetaString( windowTooltip .. "\n" .. code );

        // DRRPUtil.ACS_NamedExecute("drawPasscodeWindow");
    }

    override void NetworkProcess( ConsoleEvent e ) {
        Array<String> command;
        e.Name.Split( command, "@@@@" ); // ["opendoorinput", message, code].

        //console.printf( "NetworkProcess(). " .. e.Name );

        PlayerPawn player = players[ e.player ].mo;

        if ( ( command[ 0 ] == "drrp_opendoorinput" ) ) {
            if ( command.Size() != 3) {
                Log('Insuffiscient arguments for ' .. command[0] .. ' command! '..e.Name);
                return;
            }

            OpenPasscodeWindow(command[1], command[2], e.args[0]);
        } else if ( command[ 0 ] == "drrp_closedoorinput" ) {
            // Right password:
            if ( door.codeStr == realCodeStr || DRRPUtil.GetInternalMetaString() == "Alohomora" ) {
                OnCorrectPasscode(player);
            // Wrong password:
            } else if ( door.curinputpos == realCodeStr.Length() )  {
                OnIncorrectPasscode(player);
            }
        }

        Super.NetworkProcess( e );
    }

    override UI bool InputProcess( InputEvent e ) {
        if ( door.inputActivated && e.Type == e.Type_KeyDown ) {
            PlayerPawn player = players[ consoleplayer ].mo;
            bool redrawWindow = false;

            // Keys from '0' to '9':
            if ( e.KeyChar >= 0x30 && e.KeyChar <= 0x39 ) {
                String newStr = String.Format( "%s%c", door.codeStr.Left( door.curinputpos ), e.KeyChar );

                for( int j = door.curinputpos + 1; j < realCodeStr.Length(); j++ )
                    newStr.AppendFormat( "_" );

                door.codeStr = newStr;
                door.curinputpos++;

                if ( door.curinputpos == realCodeStr.Length() )
                    door.inputActivated = false;
                else
                    redrawWindow = true;

            // A backspace key:
            } else if ( e.KeyScan == e.Key_Backspace ) {
                if ( door.curinputpos == 0 ) {
                    door.inputActivated = false;
                } else {
                    String codeStrEndPart = "";

                    for ( int i = 0; i < door.codeStr.Length() - door.curinputpos + 1; i++ )
                        codeStrEndPart = codeStrEndPart .. "_";

                    door.codeStr.Truncate( door.curinputpos - 1 );
                    door.codeStr = door.codeStr .. codeStrEndPart;

                    door.curinputpos--;

                    redrawWindow = true;
                }

            // An escape key:
            } else if ( e.KeyScan == e.Key_Escape ) {
                door.inputActivated = false;
            }


            if ( !door.inputActivated ) {
                console.printf( "Code: \"" .. door.codeStr .. "\" (pre-finish)." );
                EventHandler.SendNetworkEvent( "drrp_closedoorinput" );
            } else if ( redrawWindow ) {
                OnRenderWindow(windowTooltip, door.codeStr);
            }
        }

        return false;
    } // of override bool InputProcess( InputEvent e ) {}

    void Log(String text) {
        Console.Printf('[APIDoorCode] ' .. text);
    }

} // of class DoorCodeInputHandler : EventHandler {}

