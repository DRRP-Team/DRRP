/**
 * Copyright (c) 2017-2022 DRRP-Team
 * 
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

// // %Name Column% - for DRRP Rebalance spreadsheet autoapply


// Dogs -------------------------------------------------------------------------------

class Hellhound: Dog_BaseDRRP replaces MBFHelperDog {
	Default {
        //$Category DMonsters
        //$Sprite DOGSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title HellHound

        Health 25; // %Dog 1%
        Tag "$DRRP_M_HELLHOUND";
        Obituary "$DRRP_OB_HELLHOUND";
	}
}

class Cerberus: Dog_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite DOGSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Cerberus

        Tag "$DRRP_M_CERBERUS";
        Obituary "$DRRP_OB_CEREBRUS";
        Health 65; // %Dog 2%
        Translation "64:79=32:47";

        ActiveSound  "monsters/cerberus/see";
        SeeSound     "monsters/cerberus/bark";
        AttackSound  "monsters/cerberus/bark";
	}

    States {
    Melee:
        DOGS EF 8 A_FaceTarget;
        DOGS G 8 A_CustomMeleeAttack( Random( 2, 5 ) * 3 + 3 );
        Goto See;
    }
}

class DemonWolf: Dog_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite DOGSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Demon Wolf

        Tag "$DRRP_M_DEMONWOLF";
        Obituary "$DRRP_OB_DEMONWOLF";
        Health 145; // %Dog 3%
        Translation "64:66=[82,89,148]:[82,89,148]", "67:70=[49,60,107]:[82,89,148]", "71:73=[49,60,107]:[49,60,107]", "74:76=[33,36,74]:[33,36,74]", "77:79=[16,16,41]:[16,16,41]", "107:107=185:185";
        Scale 1.25;

        ActiveSound "monsters/demonwolf/see";
        SeeSound    "monsters/demonwolf/bark";
        AttackSound "monsters/demonwolf/bark";
	}

    States {
    Melee:
        DOGS EF 8 A_FaceTarget;
        DOGS G 8 A_CustomMeleeAttack( Random( 2, 5 ) * 4 + 3 );
        Goto See;
    }
}


// Zombies ----------------------------------------------------------------------------

class ZombiePvt: ZombieMan_BaseDRRP replaces ZombieMan {
	Default {
        //$Category DMonsters
        //$Sprite POSSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Zombie Pvt

        Obituary "$DRRP_OB_ZOMBIEPVT";
        Tag "$DRRP_M_ZOMBIEPVT";
        Health 35; // %Zombie 1%
        Translation "16:47=152:159";

        SeeSound "monsters/zombiepvt/sight";
        PainSound "monsters/zombiepvt/pain";
        DeathSound "monsters/zombiepvt/death";
        ActiveSound "monsters/zombiepvt/active";
	}

    States {
    Missile:
        POSS E 10 A_FaceTarget;
        POSS F 8 Bright A_CustomBulletAttack( 22.5, 0, 1, Random( 3, 6 ) * 3 + 2, "BulletPuff", 0, CBAF_NORANDOM );
        POSS E 8;
        Goto See;
    }
}

class ZombieLt: ShotgunGuy_BaseDRRP replaces ShotgunGuy {
	Default {
        //$Category DMonsters
        //$Sprite POSSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Zombie Lt

        Obituary "$DRRP_OB_ZOMBIELT";
        Tag "$DRRP_M_ZOMBIELT";
        Health 75; // %Zombie 2%
        Translation "96:111=%[0.00,0.00,0.00]:[0.98,1.25,1.76]", "16:47=%[0.00,0.00,0.00]:[0.31,0.59,0.94]", "48:79=%[0.00,0.00,0.00]:[0.80,0.93,1.16]", "178:178=181:181", "168:175=193:193", "208:223=%[0.00,0.00,0.00]:[0.83,1.03,1.28]", "160:167=%[0.00,0.00,0.00]:[0.76,0.95,1.28]" ;
	}

    States {
    Missile:
        SPOS E 10 A_FaceTarget;
        SPOS F 10 Bright A_CustomBulletAttack( 22.5, 0, 3, Random( 2, 6 ) * 3 + 1, "BulletPuff", 0, CBAF_NORANDOM );
        SPOS E 10;
        Goto See;
    }
}

class ZombieCpt: ZombieMan_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite POSSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Zombie Cpt

        Obituary "$DRRP_OB_ZOMBIECPT";
        Tag "$DRRP_M_ZOMBIECPT";
        Health 155; // %Zombie 3%
        Translation "101:106=@94[144,22,16]", "26:47=@97[173,25,25]", "48:79=@19[123,16,16]", "176:191=185:185", "168:175=173:173", "160:167=%[0.00,0.00,0.00]:[1.55,0.02,0.06]", "208:223=67:67", "120:125=@52[0,128,0]";

        SeeSound "monsters/zombiecpt/sight";
        PainSound "monsters/zombiecpt/pain";
        DeathSound "monsters/zombiecpt/death";
        ActiveSound "monsters/zombiecpt/active";
	}

    States {
    Missile:
        POSS E 10 A_FaceTarget;
        POSS E 0 A_FaceTarget;
        POSS FFF 3 Bright A_CustomBulletAttack( 22.5, 0, 1, Random( 2, 6 ) * 4 + 2, "BulletPuff", 0, CBAF_NORANDOM );
        POSS E 8;
        POSS E 8 A_Jump( 195, "See" );
        Loop;
    }
}


// Chaingunners -----------------------------------------------------------------------

class Troop: ChaingunGuy_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite CPOSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Troop

        Obituary "$DRRP_OB_TROOP";
        Tag "$DRRP_M_TROOP";
        Health 45; // %Commando 1%
        Translation "32:47=163:167";

        SeeSound "monsters/troop/sight";
        PainSound "monsters/troop/pain";
        DeathSound "monsters/troop/death";
        ActiveSound "monsters/troop/active";
	}
}

class Commando: ChaingunGuy_BaseDRRP {
    const ChanceFailAttackRightBack = 192;

	Default {
        //$Category DMonsters
        //$Sprite CPOSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Commando

        Obituary "$DRRP_OB_COMMANDO";
        Tag "$DRRP_M_COMMANDO";
        Health 75; // %Commando 2%
        Translation "16:47=197:207";
	}

    States {
    Missile:
        CPOS E 10 A_FaceTarget;
        CPOS FE 4 Bright A_CustomBulletAttack( 22.5, 0, 1, Random( 1, 5 ) * 3 + 2, "BulletPuff", 0, CBAF_NORANDOM );
        CPOS F 1 A_CPosRefire;
        Goto Missile + 1;
    }
}

class Assassin: ChaingunGuy_BaseDRRP replaces ChaingunGuy {
    const ChanceFailAttackRightBack = 160;

	Default {
        //$Category DMonsters
        //$Sprite CPOSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Assassin

        Obituary "$DRRP_OB_ASSASIN";
        Tag "$DRRP_M_ASSASSIN";
        Health 170; // %Commando 3%

        SeeSound "monsters/assassin/sight";
        PainSound "monsters/assassin/pain";
        DeathSound "monsters/assassin/death";
        ActiveSound "monsters/assassin/active";
	}

    States {
    Missile:
        CPOS E 10 A_FaceTarget;
        CPOS FE 4 Bright A_CustomBulletAttack( 22.5, 0, 1, Random( 2, 7 ) * 3 + 2, "BulletPuff", 0, CBAF_NORANDOM );
        CPOS F 1 A_CPosRefire;
        Goto Missile + 1;
    }
}


// Imps -------------------------------------------------------------------------------

class Impling: DoomImp_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite TROOA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Impling

        Obituary "$DRRP_OB_IMPLING";
        HitObituary "$DRRP_OBM_IMPLING";
        Tag "$DRRP_M_IMPLING";
        Health 40; // %Imp 1%
        Scale 0.9;
        Translation "64:79=117:127", "61:63=127:127";

        SeeSound "monsters/impling/sight";
        PainSound "monsters/impling/pain";
        DeathSound "monsters/impling/death";
        ActiveSound "monsters/impling/active";
	}

    States {
        Melee:
        Missile:
            TROO EF 8 A_FaceTarget;
            TROO G 6 A_CustomComboAttack("DRRPImplingBall", 32, 1 * random(5, 8), "imp/melee");
            Goto See;
    }
}

class DRRPImp: DoomImp_BaseDRRP replaces DoomImp {
	Default {
        //$Category DMonsters
        //$Sprite TROOA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Imp

        Obituary "$DRRP_OB_IMP";
        HitObituary "$DRRP_OBM_IMP";
        Tag "$DRRP_M_IMP";
        Health 80; // %Imp 2%
	}

    States {
        Melee:
        Missile:
            TROO EF 8 A_FaceTarget;
            TROO G 6 A_CustomComboAttack("DRRPImpBall", 32, 3 * random(5, 8), "imp/melee");
            Goto See;
    }
}

class ImpLord: DoomImp_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite TROOA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Imp Lord

        Obituary "$DRRP_OB_IMPLORD";
        HitObituary "$DRRP_OBM_IMPLORD";
        Tag "$DRRP_M_IMPLORD";
        Health 165; // %Imp 3%
        Scale 1.1;
        Translation "169:191=112:127", "64:79=177:191", "191:191=127:127";

        SeeSound "monsters/implord/sight";
        PainSound "monsters/implord/pain";
        DeathSound "monsters/implord/death";
        ActiveSound "monsters/implord/active";
	}

    States {
        Melee:
        Missile:
            TROO EF 8 A_FaceTarget;
            TROO G 6 A_CustomComboAttack("DRRPImpLordBall", 32, 6 * random(5, 8), "imp/melee");
            Goto See;
    }
}


class DRRPImplingBall: DoomImpBall {
	Default {
        DamageFunction (Random( 1, 3 ) * 3 + 3);
        Scale 0.5;
	}
}

class DRRPImpBall: DoomImpBall replaces DoomImpBall {
	Default {
        DamageFunction (Random( 1, 3 ) * 6 + 3);
	}
}

class DRRPImpLordBall: DoomImpBall {
	Default {
        DamageFunction (Random( 1, 3 ) * 9 + 3);
        Scale 1.5;
	}
}


// Lost souls -------------------------------------------------------------------------

class Phantom: LostSoul_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite SKULA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Phantom

        Obituary "$DRRP_OB_PHANTOM";
        Tag "$DRRP_M_PHANTOM";
        Health 35; // %Soul 1%
        DamageFunction (Random( 1, 3 ) * 8 + 3);
        Translation "128:151=152:159", "64:79=152:159";

        AttackSound "monsters/phantom/melee";
        PainSound "monsters/phantom/pain";
        DeathSound "monsters/phantom/death";
        ActiveSound "monsters/phantom/active";
	}
}

class DRRPLostSoul: LostSoul_BaseDRRP replaces LostSoul {
	Default {
        //$Category DMonsters
        //$Sprite SKULA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Lost Soul

        Obituary "$DRRP_OB_LOSTSOUL";
        Tag "$DRRP_M_LOSTSOUL";
        Health 75; // %Soul 2%
        DamageFunction (Random( 2, 3 ) * 8 + 5);
        DamageFactor "DRRPFireExtDmg", 15.0;
	}
}

class Nightmare: LostSoul_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite SKULA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Nightmare

        Obituary "$DRRP_OB_NIGHTMARE";
        Tag "$DRRP_M_NIGHTMARE";
        Health 155; // %Soul 3%
        DamageFunction (Random( 1, 3 ) * 8 + 15);
        Translation "128:151=195:207", "64:79=200:207";
        DamageFactor "DRRPFireExtDmg", 20.0;

        AttackSound "monsters/nightmare/melee";
        PainSound "monsters/nightmare/pain";
        DeathSound "monsters/nightmare/death";
        ActiveSound "monsters/nightmare/active";
	}
}


// Demons -----------------------------------------------------------------------------

class BullDemon: Demon_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite SARGA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Bull Demon

        Obituary "$DRRP_OB_BULLDEMON";
        Tag "$DRRP_M_BULLDEMON";
        Health 60; // %Bull 1%
        Translation "213:223=162:164", "27:47=[74,65,24]:[74,65,24]", "27:37=[115,105,57]:[115,105,57]", "27:33=[156,142,99]:[156,142,99]", "21:26=[189,178,132]:[189,178,132]", "16:20=[214,203,173]:[214,203,173]";

        SeeSound "monsters/bulldemon/sight";
        AttackSound "monsters/bulldemon/melee";
        PainSound "monsters/bulldemon/pain";
        DeathSound "monsters/bulldemon/death";
        ActiveSound "monsters/bulldemon/active";
	}

    States {
    Melee:
        SARG EF 8 Fast A_FaceTarget;
        SARG G 8 Fast A_CustomMeleeAttack( 4 * Random( 1, 7 ) );
        Goto See;
    }
}

class Pinky: Demon_BaseDRRP replaces Demon {
	Default {
        //$Category DMonsters
        //$Sprite SARGA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Pinky

        Obituary "$DRRP_OB_PINKY";
        Tag "$DRRP_M_PINKY";
        Health 100; // %Bull 2%
	}

    States {
    Melee:
        SARG EF 8 Fast A_FaceTarget;
        SARG G 8 Fast A_CustomMeleeAttack( 4 * Random( 1, 10 ) );
        Goto See;
    }
}

class Belphegor: Demon_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite SARGA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Belphegor

        Obituary "$DRRP_OB_BELPHEGOR";
        Tag "$DRRP_M_BELPHEGOR";
        Health 190; // %Bull 3%
        Translation "16:47=%[0.00,0.00,0.00]:[0.57,0.77,1.41]";

        SeeSound "monsters/belphegor/sight";
        AttackSound "monsters/belphegor/melee";
        PainSound "monsters/belphegor/pain";
        DeathSound "monsters/belphegor/death";
        ActiveSound "monsters/belphegor/active";
	}

    States {
    See:
        SARG A 0;
        SARG A 0 A_SetRenderStyle( 0.33, STYLE_Fuzzy );
        Goto Super::See;
    Pain:
        SARG H 2 Fast;
        SARG H 0 A_SetRenderStyle( 0.33, STYLE_Fuzzy );
        SARG H 2 Fast A_Pain;
        Goto See;
    Melee:
        SARG EF 8 Fast A_FaceTarget;
        SARG G 8 Fast A_CustomMeleeAttack( 4 * Random( 4, 10 ) + 7 );
        Goto See;
    }
}


// Cacodemons -------------------------------------------------------------------------

class Malwrath: Cacodemon_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite HEADA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Malwrath

        Obituary "$DRRP_OB_MALWRATH";
        HitObituary "$DRRP_OBM_MALWRATH";
        Tag "$DRRP_M_MALWRATH";
        Health 50; // %Cacodemon 1%
        Translation "168:191=161:167", "16:47=160:165", "112:127=181:191", "192:207=168:191", "240:247=[0,0,0]:[176,0,0]";

        SeeSound "monsters/malwrath/sight";
        PainSound "monsters/malwrath/pain";
        DeathSound "monsters/malwrath/death";
        ActiveSound "monsters/malwrath/active";
	}

    States {
    Missile:
        HEAD BC 5 A_FaceTarget;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPMalwrathBall", 32, Random( 1, 5 ) );
        HEAD C 9;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPMalwrathBall", 32, Random( 1, 5 ) );
        HEAD C 9;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPMalwrathBall", 32, Random( 1, 5 ) );
        HEAD C 9;
        Goto See;
    }
}

class DRRPCacodemon: Cacodemon_BaseDRRP replaces Cacodemon {
	Default {
        //$Category DMonsters
        //$Sprite HEADA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Cacodemon

        Obituary "$DRRP_OB_CACODEMON";
        HitObituary "$DRRP_OBM_CACODEMON";
        Tag "$DRRP_M_CACODEMON";
        Health 80; // %Cacodemon 2%
	}

    States {
    Missile:
        HEAD BC 5 A_FaceTarget;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPCacodemonBall", 32, 10 * Random( 1, 5 ) + 5 );
        HEAD C 9 ;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPCacodemonBall", 32, 10 * Random( 1, 5 ) + 5 );
        HEAD C 9 ;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPCacodemonBall", 32, 10 * Random( 1, 5 ) + 5 );
        HEAD C 9 ;
        Goto See;
    }
}

class Wretched: Cacodemon_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite HEADA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Wretched

        Obituary "$DRRP_OB_WRETCHED";
        HitObituary "$DRRP_OBM_WRETCHED";
        Tag "$DRRP_M_WRETCHED";
        Health 175; // %Cacodemon 3%
        Translation "168:191=193:207", "167:167=244:244", "16:47=193:201", "192:207=168:184", "240:242=185:191", "223:223=207:207", "0:2=246:246";

        SeeSound "monsters/wretched/sight";
        PainSound "monsters/wretched/pain";
        DeathSound "monsters/wretched/death";
        ActiveSound "monsters/wretched/active";
	}

    States {
    Missile:
        HEAD BC 5 A_FaceTarget;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPWretchedBall", 32, 10 * Random( 2, 8 ) );
        HEAD C 9 ;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPWretchedBall", 32, 10 * Random( 2, 8 ) );
        HEAD C 9 ;
        HEAD D 4 Bright A_CustomComboAttack( "DRRPWretchedBall", 32, 10 * Random( 2, 8 ) );
        HEAD C 9 ;
        Goto See;
    }
}

class DRRPMalwrathBall: CacodemonBall {
	Default {
        DamageFunction (Random( 2, 5 ));
        Translation "168:180=224:231", "181:191=160:167", "253:253=164:164", "47:47=162:162", "208:223=224:231";
        Scale 0.8;
	}
}

class DRRPCacodemonBall: CacodemonBall replaces CacodemonBall {
	Default {
        DamageFunction (4 * Random( 3, 10 ) + 7);
        Scale 1.0;
	}
}

class DRRPWretchedBall: CacodemonBall {
	Default {
        DamageFunction (7 * Random( 5, 10 ) + 10);
        Translation "168:191=193:207", "167:167=244:244", "16:47=193:201", "192:207=168:184", "240:242=185:191", "223:223=207:207", "0:2=246:246";
        Scale 1.2;
	}
}


// Revenants --------------------------------------------------------------------------

class Ghoul: Revenant_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite SKELA1D1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Ghoul

        Obituary "$DRRP_OB_GHOUL";
        HitObituary "$DRRP_OBM_GHOUL";
        Tag "$DRRP_M_GHOUL";
        Health 70; // %Revenant 1%
        Translation "133:138=[115,48,49]:[181,117,115]", "80:87=@100[181,117,115]", "88:92=@100[156,65,66]", "93:95=@100[115,48,49]", "4:4=@42[181,117,115]", "32:35=[24,134,16]:[24,134,16]", "36:39=[24,134,16]:[24,134,16]", "40:47=[41,105,8]:[41,105,8]", "232:235=[41,105,8]:[60,158,11]", "128:138=@100[179,74,75]", "128:167=@50[179,74,75]";

        SeeSound "monsters/ghoul/sight";
        PainSound "monsters/ghoul/pain";
        DeathSound "monsters/ghoul/death";
        ActiveSound "monsters/ghoul/active";
	}

    States {
    Melee:
        SKEL G 0 A_FaceTarget;
        SKEL G 6 A_SkelWhoosh;
        SKEL H 6 A_FaceTarget;
        SKEL I 6 A_CustomMeleeAttack( Random( 1, 8 ) * 4, "skeleton/melee" );
        Goto See;
    }
}

class Fiend: Revenant_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite SKELA1D1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Fiend

        Obituary "$DRRP_OB_FIEND";
        HitObituary "$DRRP_OBM_FIEND";
        Tag "$DRRP_M_FIEND";
        Health 100; // %Revenant 2%
        Translation "80:87=@77[33,32,74]", "88:92=@85[33,32,74]", "93:95=@77[33,32,74]", "4:4=@41[33,32,74]", "32:39=[132,16,82]:[153,18,95]", "40:47=[107,12,90]:[107,12,90]", "232:235=[153,18,95]:[191,22,119]", "128:143=@77[48,55,140]", "144:151=@100[48,55,140]", "152:159=@100[57,65,165]", "160:167=@60[57,65,165]", "64:79=[102,139,163]:[27,26,64]";
	}

    States {
    Melee:
        SKEL G 0 A_FaceTarget;
        SKEL G 6 A_SkelWhoosh;
        SKEL H 6 A_FaceTarget;
        SKEL I 6 A_CustomMeleeAttack( Random( 1, 10 ) * 5, "skeleton/melee" );
        Goto See;
    }
}

class DRRPRevenant: Revenant_BaseDRRP replaces Revenant { // Huesos (Spanish)
	Default {
        //$Category DMonsters
        //$Sprite SKELA1D1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Revenant

        Obituary "$DRRP_OB_REVENANT";
        HitObituary "$DRRP_OBM_REVENANT";
        Tag "$DRRP_M_REVENANT";
        Health 200; // %Revenant 3%

        SeeSound "monsters/revenant/sight";
        PainSound "monsters/revenant/pain";
        DeathSound "monsters/revenant/death";
        ActiveSound "monsters/revenant/active";
	}

    States {
    Melee:
        SKEL G 0 A_FaceTarget;
        SKEL G 6 A_SkelWhoosh;
        SKEL H 6 A_FaceTarget;
        SKEL I 6 A_CustomMeleeAttack( Random( 3, 15 ) * 6, "skeleton/melee" );
        Goto See;
    }
}



// Pain Elementals --------------------------------------------------------------------
// [McM]: Really? The mightiest pain elemental has least DoomEdNum?

class Beholder: PainElemental_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite PAINA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Beholder

        Obituary "$DRRP_OB_BEHOLDER";
        HitObituary "$DRRP_OBM_BEHOLDER";
        Tag "$DRRP_M_BEHOLDER";
        Health 60; // %Pain Elemental 1%
        Translation "128:143=%[0.00,0.00,0.00]:[0.92,1.18,0.66]", "13:15=9:10", "64:79=%[0.00,0.00,0.00]:[0.97,1.20,0.62]", "13:15=%[0.00,0.00,0.00]:[0.80,1.02,0.51]", "236:239=%[0.00,0.00,0.00]:[0.66,0.83,0.49]", "1:2=12:12", "167:167=12:12", "165:165=159:159";

        SeeSound "monsters/beholder/sight";
        PainSound "monsters/beholder/pain";
        DeathSound "monsters/beholder/death";
        ActiveSound "monsters/beholder/active";
	}

    States {
    Death:
        PAIN H 8 Bright;
        PAIN I 8 Bright {
            A_Scream();
            A_NoBlocking();
            A_DRRP_PainSpawnSoul( "Phantom" );
        }
        PAIN J 8 Bright;
        PAIN K 8 Bright A_DRRP_PainSpawnSoul( "Phantom" );
        PAIN L 8 Bright;
        PAIN M 8 Bright A_DRRP_PainSpawnSoul( "Phantom" );
        PAIN Z -1;
        Stop;
    }
}

class Rahovart: PainElemental_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite PAINA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Rahovart

        Obituary "$DRRP_OB_RAHOVART";
        HitObituary "$DRRP_OBM_RAHOVART";
        Tag "$DRRP_M_RAHOVART";
        Health 90; // %Pain Elemental 2%
        Translation "128:143=%[0.00,0.00,0.00]:[0.39,0.53,1.72]", "13:15=9:10", "64:79=%[0.00,0.00,0.00]:[0.47,0.61,1.79]", "13:15=%[0.00,0.00,0.00]:[0.12,0.22,1.14]", "236:239=%[0.00,0.00,0.00]:[0.22,0.31,1.01]", "1:2=12:12", "167:167=12:12", "165:165=159:159";
	}

    States {
    Death:
        PAIN H 8 Bright;
        PAIN I 8 Bright {
            A_Scream();
            A_NoBlocking();
            A_DRRP_PainSpawnSoul( "DRRPLostSoul" );
        }
        PAIN J 8 Bright;
        PAIN K 8 Bright A_DRRP_PainSpawnSoul( "DRRPLostSoul" );
        PAIN L 8 Bright;
        PAIN M 8 Bright A_DRRP_PainSpawnSoul( "DRRPLostSoul" );
        PAIN Z -1;
        Stop;
    }
}

class DRRPPainElemental: PainElemental_BaseDRRP replaces PainElemental {
	Default {
        //$Category DMonsters
        //$Sprite PAINA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Pain Elemental

        Obituary "$DRRP_OB_PAINELEMENTAL";
        HitObituary "$DRRP_OBM_PAINELEMENTAL";
        Tag "$DRRP_M_PAINELEMENTAL";
        Health 185; // %Pain Elemental 3%

        SeeSound "monsters/painelemental/sight";
        PainSound "monsters/painelemental/pain";
        DeathSound "monsters/painelemental/death";
        ActiveSound "monsters/painelemental/active";
	}

    States {
    Death:
        PAIN H 8 Bright;
        PAIN I 8 Bright {
            A_Scream();
            A_NoBlocking();
            A_DRRP_PainSpawnSoul( "Nightmare" );
        }
        PAIN J 8 Bright;
        PAIN K 8 Bright A_DRRP_PainSpawnSoul( "Nightmare" );
        PAIN L 8 Bright;
        PAIN M 8 Bright A_DRRP_PainSpawnSoul( "Nightmare" );
        PAIN Z -1;
        Stop;
    }
}



// ManCubuses -------------------------------------------------------------------------
// Dull joke by UsernameAK: "Minecraft -- cube world".
// <PROPHESSOR> *laughs*

class Behemoth: Fatso_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite FATTA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Behemoth

        Obituary "$DRRP_OB_BEHEMOTH";
        Tag "$DRRP_M_BEHEMOTH";
        Health 90; // %Mancubus 1%
        Translation "48:79=%[0.00,0.00,0.00]:[0.95,0.89,1.83]", "128:143=%[0.00,0.00,0.00]:[1.02,1.04,1.57]", "112:127=176:191", "160:167=%[0.00,0.00,0.00]:[1.02,0.94,1.91]", "212:223=%[0.00,0.00,0.00]:[0.90,0.82,1.84]", "13:15=%[0.00,0.00,0.00]:[0.83,0.83,2.00]", "236:239=%[0.00,0.00,0.00]:[0.83,0.83,2.00]", "144:151=%[0.00,0.00,0.00]:[0.83,0.83,2.00]", "176:191=%[0.00,0.00,0.00]:[0.83,0.83,2.00]", "16:47=%[0.00,0.00,0.00]:[2.00,0.00,0.00]";

        SeeSound "monsters/behemoth/sight";
        PainSound "monsters/behemoth/pain";
        DeathSound "monsters/behemoth/death";
        ActiveSound "monsters/behemoth/active";
	}
}

class Mancubus: Fatso_BaseDRRP replaces Fatso {
	Default {
        //$Category DMonsters
        //$Sprite FATTA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Mancubus

        Obituary "$DRRP_OB_MANCUBUS";
        Tag "$DRRP_M_MANCUBUS";
        Health 120; // %Mancubus 2%
	}
}

class Druj: Fatso_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite FATTA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Druj

        Tag "$DRRP_M_DRUJ";
        Obituary "$DRRP_OB_DRUJ";
        Health 220; // %Mancubus 3%
        Translation "48:79=16:45", "144:151=16:45", "144:151=16:45", "236:239=46:46", "13:15=46:46", "141:141=46:46", "138:138=46:46", "142:142=45:45", "143:143=45:45", "16:47=176:189";

        SeeSound "monsters/druj/sight";
        PainSound "monsters/druj/pain";
        DeathSound "monsters/druj/death";
        ActiveSound "monsters/druj/active";
	}
}

class DRRPFatShot: FatShot replaces FatShot {
	Default {
        DamageFunction (8 * Random( 1, 8 ));
	}
}

// Arch-Viles -------------------------------------------------------------------------

class Infernis: ArchVile_BaseDRRP {
    const vileInitialDamage = 20;

	Default {
        //$Category DMonsters
        //$Sprite VILEA1D1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Infernis

        Obituary "$DRRP_OB_INFERNIS";
        Tag "$DRRP_M_INFERNIS";
        Health 55; // %Vile 1%
        Translation  "48:79=192:207", "128:143=192:207", "13:15=240:247", "144:151=240:247", "236:239=240:247", "144:151=240:247", "208:223=192:207", "0:2=247:247";

        SeeSound "monsters/infernis/sight";
        PainSound "monsters/infernis/pain";
        DeathSound "monsters/infernis/death";
        ActiveSound "monsters/infernis/active";
	}
}

class DRRPArchVile: ArchVile_BaseDRRP replaces ArchVile {
    const vileInitialDamage = 40;

	Default {
        //$Category DMonsters
        //$Sprite VILEA1D1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title ArchVile

        Obituary "$DRRP_OB_ARCHVILE";
        Tag "$DRRP_M_ARCHVILE";
        Health 85; // %Vile 2%
	}
}

class Apollyon: ArchVile_BaseDRRP {
    const vileInitialDamage = 60;

	Default {
        //$Category DMonsters
        //$Sprite VILEA1D1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Apollyon

        Obituary "$DRRP_OB_APOLLYON";
        Tag "$DRRP_M_APOLLYON";
        Health 180; // %Vile 3%
        Translation "64:79=32:47", "48:63=16:31", "128:143=16:39", "236:239=44:47", "13:15=45:47", "144:151=32:39"; // "16:223=%[0.00, 0.00, 0.00]:[2.00, 0.78, 0.78]"

        SeeSound "monsters/apollyon/sight";
        PainSound "monsters/apollyon/pain";
        DeathSound "monsters/apollyon/death";
        ActiveSound "monsters/apollyon/active";
	}
}


// Barons of Hell ---------------------------------------------------------------------

// [McM]: Really? The mightiest baron of hell has least DoomEdNum?
// [PROPHESSOR]: Yeah... But won't fix)

class Ogre: HellKnight_BaseDRRP {
	Default {
        //$Category DMonsters
        //$Sprite BOS2A1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Ogre

        Obituary "$DRRP_OB_OGRE";
        HitObituary "$DRRP_OBM_OGRE";
        Tag "$DRRP_M_OGRE";
        Health 80; // %Baron 1%
        Translation "112:127=176:191", "128:143=%[0.00,0.00,0.00]:[0.77,0.82,0.64]", "144:151=%[0.00,0.00,0.00]:[0.80,0.85,0.65]", "48:63=%[0.00,0.00,0.00]:[0.76,0.82,0.64]", "13:15=%[0.00,0.00,0.00]:[0.65,0.69,0.54]";

        SeeSound "monsters/ogre/sight";
        ActiveSound "monsters/ogre/active";
        PainSound "monsters/ogre/pain";
        DeathSound "monsters/ogre/death";
	}

    States {
        Melee:
        Missile:
            BOS2 EF 8 A_FaceTarget;
            BOS2 G 8 A_CustomComboAttack( "DRRPOgreBall", 32, 2 * Random( 4, 7 ), "baron/melee" );
            Goto See;
    }
}

class DRRPHellKnight: HellKnight_BaseDRRP replaces HellKnight {
	Default {
        //$Category DMonsters
        //$Sprite BOS2A1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Hell Knight

        Obituary "$DRRP_OB_HELLKNIGHT";
        HitObituary "$DRRP_OBM_HELLKNIGHT";
        Tag "$DRRP_M_HELLKNIGHT";
        Health 110; // %Baron 2%
	}

    States {
    Melee:
    Missile:
        BOS2 EF 8 A_FaceTarget;
        BOS2 G 8 A_CustomComboAttack( "DRRPHellKnightBall", 32, 4 * Random( 4, 7 ), "baron/melee" );
        Goto See;
    }
}

class DRRPBaronOfHell: BaronOfHell_BaseDRRP replaces BaronOfHell {
	Default {
        //$Category DMonsters
        //$Sprite BOSSA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Baron

        Obituary "$DRRP_OB_BARON";
        HitObituary "$DRRP_OBM_BARON";
        Tag "$DRRP_M_BARON";
        Health 210; // %Baron 3%

        Translation "112:127=160:167";
	}

    States {
        Melee:
        Missile:
            BOSS EF 8 A_FaceTarget;
            BOSS G 8 A_CustomComboAttack( "DRRPBaronBall", 32, 6 * Random( 4, 7 ), "baron/melee" );
            Goto See;
    }
}

class DRRPOgreBall: BaronBall {
	Default {
        DamageFunction (2 * Random( 5, 8 ));
        Translation "112:127=176:191", "128:143=%[0.00,0.00,0.00]:[0.77,0.82,0.64]", "144:151=%[0.00,0.00,0.00]:[0.80,0.85,0.65]", "48:63=%[0.00,0.00,0.00]:[0.76,0.82,0.64]", "13:15=%[0.00,0.00,0.00]:[0.65,0.69,0.54]";
	}
}

class DRRPHellKnightBall: BaronBall {
	Default {
        DamageFunction (4 * Random( 5, 8 ));
	}
}

class DRRPBaronBall: BaronBall replaces BaronBall {
	Default {
        DamageFunction (8 * Random( 5, 8 ));
        Translation "112:127=160:167";
	}
}



// Cyberdemon & Kronos ----------------------------------------------------------------

class DRRPCyberdemon : Cyberdemon replaces Cyberdemon {
	Default {
        //$Category DMonsters
        //$Sprite CYBRA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Cyberdemon

        +LOOKALLAROUND;

        //*Sound "monsters/cyberdemon/*"

        Obituary "$DRRP_OB_CYBERDEMON";
        Tag "$DRRP_M_CYBERDEMON";
        Health 1666; // %Cyberdemon 1%
	}
}

class Kronos: Actor {
	Default {
        //$Category DMonsters
        //$Sprite KRONA1
        //$Colour RGB(200, 0, 0)
        //$Angled
        //$Title Kronos

        MONSTER;
        +LOOKALLAROUND;
        +BOSS;
        +NOGRAVITY;
        +FLOAT;
        Radius      24;
        Height      56;
        Health      666; // %Kronos 1% // Original: 344..572
        PainChance  127;
        Speed       20;
        Scale       0.43;
        Obituary    "$DRRP_OB_KRONOS";
        Tag         "$DRRP_M_KRONOS";

        SeeSound    "monsters/kronos/see";
        ActiveSound "monsters/kronos/active";
        PainSound   "monsters/kronos/pain";
        DeathSound  "monsters/kronos/death";
	}

    States {
        Spawn:
            KRON A 2 A_Look;
            Wait;
        See:
            KRON AAA 2 {
                A_Chase();
                return A_Jump( 8, "KronosTeleport" );
            }
            KRON A 0 A_SetScale( -0.43, 0.43 );
            KRON AAA 2 {
                A_Chase();
                return A_Jump( 8, "KronosTeleport" );
            }
            KRON A 0 A_SetScale( 0.43, 0.43 );
            Loop;
        KronosTeleport:
            KRON A 0;
            KRON A 0 A_SpawnItem( "DRRPKronosTeleportFog" );
            KRON AAAAAAAAAAAAAA 0 A_Wander;
            KRON A 0 A_SpawnItem( "DRRPKronosTeleportFog" );
            Goto See;
        Melee:
            KRON A 2;
            KRON A 5 A_CustomMeleeAttack( Random( 1, 10 ), "monsters/kronos/meleehit", "monsters/kronos/meleemiss" );
            KRON A 3;
            Goto See;
        Pain:
            KRON A 0 A_Pain;
            Goto See;
        Death:
            KRON M 3;
            KRON N 2 A_Scream();
            KRON O 2 A_NoBlocking();
            KRON P 2;
            KRON QRST 3;
            KRON UVWX 2;
            KRON Y 15;
            KRON Z -1;
            Stop;
    }
}
