/**
 *Copyright (c) 2018-2019 DRRP-Team
 *
 *This software is released under the MIT License.
 *https://opensource.org/licenses/MIT
 */

enum ELevelStatsNotifier {
    LSN_Unknown         = 0x00,
    LSN_AllMonsters     = 0x01,
    LSN_AllSecrets      = 0x02,
    LSN_Both            = LSN_AllMonsters | LSN_AllSecrets,
}

class LevelStatsNotifierHandler: StaticEventHandler {

    Array<String> MapsMonstersDone;
    Array<String> MapsSecretsDone;
    uint curMapDoneMask;

    // ZScript cannot return a pointer to the dynamic array, 
    //so "ui Array<String> GetAllDoneMapNamesArray()" is impossible.
    ui String GetAllDoneMapNamesArray( int doneType, String outIfNone = "", String separator = "\n" ) {
        String outStr = "";
        int arraySize;

        switch ( doneType ) {
            case LSN_AllMonsters:
                arraySize = MapsMonstersDone.Size();

                for ( int i = 0; i < arraySize - 1; i++ )
                    outStr = outStr .. MapsMonstersDone[ i ] .. separator;

                if ( arraySize )
                    outStr = outStr .. MapsMonstersDone[ arraySize - 1 ];

                break;
            case LSN_AllSecrets:
                arraySize = MapsSecretsDone.Size();

                for ( int i = 0; i < arraySize - 1; i++ )
                    outStr = outStr .. MapsMonstersDone[ i ] .. separator;

                if ( arraySize )
                    outStr = outStr .. MapsSecretsDone[ arraySize - 1 ];

                break;
            default:
                break;
        } // of switch ( doneType ) {

        return ( outStr == "" )? outIfNone : outStr;
    } // of ui String GetAllDoneMapNamesArray( int doneType, String outIfNone = "", String separator = "\n" ) {


    override void WorldLoaded( WorldEvent e ) {
        // Check for new game:
        if ( !level.totaltime ) {
            MapsMonstersDone.Clear();
            MapsSecretsDone.Clear();
        }

        curMapDoneMask  = LSN_AllMonsters * ( level.total_monsters == level.killed_monsters );
        curMapDoneMask |= LSN_AllSecrets  * ( level.total_secrets == level.found_secrets );

        Super.WorldLoaded( e );
    }

    override void WorldTick() {
        if ( !( level.maptime % 3 ) ) {
            bool callDoneSequence = false;

            // All monsters killed check:
            if ( !( curMapDoneMask & LSN_AllMonsters ) && level.total_monsters == level.killed_monsters ) {
                MapsMonstersDone.Push( level.LevelName );
                curMapDoneMask |= LSN_AllMonsters;
                callDoneSequence = true;
            }

            // All secrets found check:
            if ( !( curMapDoneMask & LSN_AllSecrets ) && level.total_secrets == level.found_secrets ) {
                MapsSecretsDone.Push( level.LevelName );
                curMapDoneMask |= LSN_AllSecrets;
                callDoneSequence = true;
            }

            // Done sequence:
            if ( callDoneSequence && playeringame[ 0 ] && players[ 0 ].mo ) {
                DRRPPlayer player = DRRPPlayer( players[ 0 ].mo );

                // If secrets OR monsters OR both:
                if ( curMapDoneMask & LSN_Both )
                    player.AddFeature( RPG_Experience, 10 );

                // If both:
                if ( ( curMapDoneMask & LSN_Both ) == LSN_Both )
                    player.GiveInventory( "Credit", 25 );

                player.ACS_ScriptCall( "LevelDoneNotifierVisual" );
            }

        } // of if ( !( level.maptime % 3 ) ) {

        Super.WorldTick();
    }

} // of class LevelStatsNotifierHandler: StaticEventHandler {



class StatInfoMenu: OptionMenu {
    enum EColors {
        cNormal = Font.CR_GRAY,
        cBad    = Font.CR_RED,
        cGood   = Font.CR_DARKGREEN,
        cHeader = Font.CR_BLUE
    };

    OptionMenuDescriptor initdesc;


    String StatLocalize( String postfix ) {
        return StringTable.Localize( "$DRRP_D_STATINFO_" .. postfix );
    } // of String StatLocalize( String postfix ) {

    protected void addStr( int textColor, String text ) {
        OptionMenuItemStaticText menuitem = new( "OptionMenuItemStaticText" );
        menuitem.Init( text );
        menuitem.mColor = textColor;
        initdesc.mItems.push( menuitem );
    } // of protected void addStr( String text ) {

    protected void addItemStr( Inventory item, String text, bool sizeless = false ) {
        if ( item == null ) 
            text = text .. "0";
        else if ( sizeless )
            text = text .. item.Amount;
        else
            text = text .. item.Amount .. "/" .. item.MaxAmount;

        addStr( ( item == null ) || ( !item.amount ) ? cBad : 
                    ( item.Amount >= item.MaxAmount ? cGood : cNormal ), text );
    } // of protected void addItemStr( Inventory item, String text, bool sizeless = false ) {

    protected void addSpecIntStr( int checkInt, int maxInt, String text, bool bad_checking = false ) {

        addStr( ( checkInt <= 0 ) && bad_checking ? cBad : 
                    ( ( checkInt >= maxInt ) ? cGood : cNormal ), 
                text .. checkInt .. "/" .. maxInt );
    } // of protected void addSpecIntStr( int checkInt, int maxInt, String text, bool bad_check = false ) {

    protected void addDoneLevelsStr( uint doneType ) {
        LevelStatsNotifierHandler mapsHandler = LevelStatsNotifierHandler( EventHandler.Find( "LevelStatsNotifierHandler" ) );
        String rawMapnames = mapsHandler.GetAllDoneMapNamesArray( doneType, "", "\n" );

        if ( rawMapnames.Length() ) {
            Array<String> mapnames;
            rawMapnames.Split( mapnames, "\n" );

            for ( int i = 0; i < mapnames.Size(); i++ )
                addStr( cNormal, mapnames[ i ] );
        } else {
            addStr( cNormal, StatLocalize( "NOTHING" ) );
        }
    } // of protected void addDoneLevels( uint doneType ) {

    protected void addHeaderStr( String headerText ) {
        int headerMaxLength = 20;
        addStr( cNormal, " " );

        headerText = " " .. headerText .. " ";
        while ( headerText.Length() < headerMaxLength ) 
            headerText = "-" .. headerText .. "-";

        addStr( cHeader, headerText );
    } // of protected void addHeaderStr( String text ) {




    override void Init( Menu parent, OptionMenuDescriptor desc ) {
        Super.Init( parent, desc );

        PlayerInfo pStatInfo = players[ consoleplayer ];
        initdesc = desc;
        initdesc.mItems.clear();

        if ( !pStatInfo || !pStatInfo.mo || ( gamestate != GS_LEVEL ) ) {
            addStr( cNormal, "$DRRP_D_STATINFO_NOTINGAME" );
            return;
        }

        DRRPPlayer player = DRRPPlayer( pStatInfo.mo );

        //String notNow = StatLocalize( "UNDERCONSTRUCT" ); // Finally, got it!

        addHeaderStr( StatLocalize( "PLAYER_T" ) );
        addSpecIntStr( player.health, player.MaxHealth, StatLocalize( "HEALTH" ), true );
        addItemStr( player.FindInventory( "DRRPBasicArmor" ), StatLocalize( "ARMOR" ) );
        addItemStr( player.FindInventory( "Credit" ), StatLocalize( "CREDITS" ), true );
        addStr( cNormal, StatLocalize( "LEVEL" ) .. player.GetFeature( RPG_Level ) );
        addSpecIntStr( player.GetFeature( RPG_Experience ), player.GetFeature( RPG_NextLevelXP ), StatLocalize( "EXPERIENCE" ) );
        addStr( cNormal, StatLocalize( "DEFENCE" ) .. player.GetFeature( RPG_Defence ) );
        addStr( cNormal, StatLocalize( "STRENGTH" ) .. player.GetFeature( RPG_Strength ) );
        addStr( cNormal, StatLocalize( "AGILITY" ) .. player.GetFeature( RPG_Agility ) );
        addStr( cNormal, StatLocalize( "ACCURACY" ) .. player.GetFeature( RPG_Accuracy ) );

        addHeaderStr( StatLocalize( "THISSECTOR_T" ) );
        addStr( cNormal, StatLocalize( "TIME" ) .. level.maptime / 35 );
        addSpecIntStr( level.found_secrets, level.total_secrets, StatLocalize( "SECRETS" ) );
        addSpecIntStr( level.killed_monsters, level.total_monsters, StatLocalize( "MONSTERS" ) );
        addSpecIntStr( level.found_items, level.total_items, StatLocalize( "ITEMS" ) );

        addHeaderStr( StatLocalize( "TOTAL_T" ) );
        addStr( cNormal, StatLocalize( "TIME" ) .. level.totaltime / 35 );

        addHeaderStr( StatLocalize( "MAXKILLS_T" ) );
        addDoneLevelsStr( LSN_AllMonsters );

        addHeaderStr( StatLocalize( "MAXSECRETS_T" ) );
        addDoneLevelsStr( LSN_AllSecrets );

    } // override void Init( Menu parent, OptionMenuDescriptor desc ) {

} // of class StatInfoMenu: OptionMenu {
