/**
 * Copyright (c) 2019 DRRP-Team
 * 
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

class DRRPUtil {
    const e = 2.71828182845904523536;

    // Returns (digit^power).
    //(I think there's no situations in project where I may use imaginary numbers).
    static double Power( double digit, double power ) {
        double outValue = 1.0;

        if ( power != 0.0 ) {
            if ( digit > 0.0 )
                exp( power * log( digit ) );
            else if ( digit == 0.0 )
                outValue = 0.0;
            else
                outValue = exp( power * log( abs( digit ) ) ) * ( power > 0.0? 1 : -1 );
        }

        return outValue;
    } // of static double Power( double digit, double power ) {}


    // The higher the value this constant has, the slower the particle speed.
	//It is neccesary only in two similar cases in th whole project, so a
	//constant definition is enough.
    const velocityParticlesDelay = 4.0;

    static play Actor SummonDRRPParticle( Actor spawner, name spawnee, double splatXY, double splatZ ) {
        Actor particle = Actor.Spawn( spawnee, (
                                spawner.pos.x + Random( splatXY * 0.5, splatXY * 1.5 ) * RandomPick( -1, 1 ),
                                spawner.pos.y + Random( splatXY * 0.5, splatXY * 1.5 ) * RandomPick( -1, 1 ),
                                spawner.pos.z + Random( splatZ * 0.05, splatZ * 0.95 ) ) );

        particle.Vel = ( ( spawner.pos.x - particle.pos.x ) / velocityParticlesDelay,
                         ( spawner.pos.y - particle.pos.y ) / velocityParticlesDelay,
                         ( ( spawner.pos.z + splatZ / 2 ) - particle.pos.z ) / velocityParticlesDelay );

        return particle;
    } // of static play Actor SummonDRRPParticle( Actor spawner, name spawnee, double splatXY, double splatZ ) {}

    const INTERNAL_META_STRING_KEY = "drrp_internal_metastring";

	// A string buffer, same for ZScript and ACS:
	static void SetInternalMetaString( String str ) {
		CVar.GetCVar( INTERNAL_META_STRING_KEY ).SetString( str );
	}
	static String GetInternalMetaString( void ) {
		return CVar.GetCVar( INTERNAL_META_STRING_KEY ).GetString();
	}

    static void ACS_NamedExecute( String scriptName, int arg1=0, int arg2=0, int arg3=0) {
        ACS_Execute(-int(name(scriptName)), 0, arg1, arg2, arg3);
    }

} // of class DRRPUtil {}



// Some UsernameAK's data classes:

class DRRPIntToStringBruteforceMultiMapArrayWrapper {
    Array<String> TheStringArray;
}

class DRRPIntToStringBruteforceMultiMap {
    private Array<int> TheIntArray;
    private Array<DRRPIntToStringBruteforceMultiMapArrayWrapper> TheStringArrayArray;
    
    void Put(int key, String value) {
        for(int i = 0; i < TheIntArray.Size(); i++) {
            if(key == TheIntArray[i]) {
                if(TheStringArrayArray[i].TheStringArray.Find(value) == TheStringArrayArray[i].TheStringArray.Size()) {
                    TheStringArrayArray[i].TheStringArray.Push(value);
                }
                return;
            }
        }
        TheIntArray.Push(key);
        DRRPIntToStringBruteforceMultiMapArrayWrapper TheWrapper = new("DRRPIntToStringBruteforceMultiMapArrayWrapper");
        TheWrapper.TheStringArray.Push(value);
        TheStringArrayArray.Push(TheWrapper);
    }
    
    DRRPIntToStringBruteforceMultiMapArrayWrapper Get(int key) {
        for(int i = 0; i < TheIntArray.Size(); i++) {
            if(key == TheIntArray[i]) {
                return TheStringArrayArray[i];
            }
        }
        DRRPIntToStringBruteforceMultiMapArrayWrapper TheWrapper = new("DRRPIntToStringBruteforceMultiMapArrayWrapper");
        return TheWrapper;
    }
}
