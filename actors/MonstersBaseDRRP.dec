//

/*  == Drop items: ==

// See "/ZScript/Patches.zsc :: DRRPGeneralRandomSpawner".
I  ) 20% || (is 'Dog' / 'Cyber' / 'Kronos'): NULL

II ) 40%:
    1) 33% Health bonus;
    2) 33% Armor bonus;
    3) 33% Credit:
        a) (monsLvl==1 && 1%): gold credit;
        b) (monsLvl==2 && 3%): gold credit;
        c) (monsLvl==3 && 5%): gold credit;
        d) Green credit.


// Part III is in this file.
III) 40%:
    1) (is 'Zombieman'):
        a) 6%: clips 4x;
        b) 30%: shell;
        c) 64%: clip.
    2) (is 'Chaingunner'):
        a) 33%: clips 4x;
        b) 67%: clip.
    3) (is 'Cacodemon'):
        a) 6%: large medikit;
        b) 94%: small medikit.
    4) (is 'PainElemental'):
        100%: gold credit.
    5) (is 'Revenant'):
        a) 6%: rockets 4x;
        b) 94%: rocket.
    6) (is 'Fatso'):
        a) 6%: cells 4x;
        b) 94%: cell.
    7) (is 'ArchVile'):
        a) 30%: gold credit;
        b) 4%: large medikit;
        c) 66%: small medikit.
    8) (is 'Baron'):
        a) 30%: cells 4x;
        a) 70%: shells 4x;
    9) (is 'Pinky'):
        a) 0%: NULL.
*/


Actor Dog_BaseDRRP: MBFHelperDog {
    +LOOKALLAROUND

    Speed 8 // 10

    DamageFactor DRRPShotgunDmg, 4.0
    DamageFactor DRRPPlasmaDmg, 2.0

    ActiveSound  "monsters/hellhound/see"
    PainSound    "monsters/hellhound/pain"
    DeathSound   "monsters/hellhound/death"
    SeeSound     "monsters/hellhound/bark"
    AttackSound  "monsters/hellhound/bark"

    States {
    Melee:
        DOGS EF 8 A_FaceTarget
        DOGS G 8 A_CustomMeleeAttack( Random( 1, 5 ) * 3 + 2 )
        Goto See
    }
}

Actor ZombieMan_BaseDRRP: ZombieMan {
    DropItem "Clip", 0, 0
    +LOOKALLAROUND

    DamageFactor DRRPAxeDmg, 3.0

    States {
    Missile:
        POSS E 10 A_FaceTarget
        POSS F 8 A_CustomBulletAttack( 22.5, 0, 1, Random( 1, 5 ) * 3, "BulletPuff", 0, CBAF_NORANDOM )
        POSS E 8
        Goto See
    }
}

Actor ShotgunGuy_BaseDRRP: ShotgunGuy {
    DropItem "DRRPShell", 0, 0
    +LOOKALLAROUND

    DamageFactor DRRPAxeDmg, 3.0

    States {
    Missile:
        SPOS E 10 A_FaceTarget
        SPOS F 10 A_CustomBulletAttack( 22.5, 0, 3, Random( 1, 5 ) * 3 - 1, "BulletPuff", 0, CBAF_NORANDOM )
        SPOS E 10
        Goto See
    }
}

Actor ChaingunGuy_BaseDRRP: ChaingunGuy {
    const int ChanceFailAttackRightBack = 224;

    DropItem "Chaingun", -1, 0
    DamageFactor DRRPBulletDmg, 3.0
    DamageFactor DRRPPlasmaDmg, 2.0
    +LOOKALLAROUND

    States {
    Missile:
        CPOS E 10 A_FaceTarget
        CPOS FE 4 Bright A_CustomBulletAttack( 22.5, 0, 1, Random( 1, 4 ) * 3 - 2, "BulletPuff", 0, CBAF_NORANDOM )
        CPOS F 2 A_CPosRefire
        Goto Missile + 1
    Pain:
        CPOS G 3 A_Pain
        CPOS G 0 A_JumpIfCloser( 196, 1 )
        Goto PainOut
        CPOS G 2 A_Jump( ChanceFailAttackRightBack, "PainOut" )
        Goto Missile + 1
    PainOut:
        CPOS G 1
        Goto See
    }
}

Actor DoomImp_BaseDRRP: DoomImp {
    +LOOKALLAROUND

    DamageFactor DRRPShotgunDmg, 3.0
}

Actor LostSoul_BaseDRRP: LostSoul {
    Health 150
    Species "LostSoul_BaseDRRP, Phantom, DRRPLostSoul, Nightmare"

    Damage (Random( 1, 3 ) * 8 + 3)
    +LOOKALLAROUND
    -DONTFALL
    +NOINFIGHTSPECIES
    +BRIGHT

    DamageFactor DRRPFireExtDmg, 10.0
    DamageFactor DRRPFireDmg, 0.0

    States {
    See:
        SKUL AABB 3 A_Chase
        Loop
    Death:
        SKUL F 6 Bright
        SKUL G 6 Bright A_Scream
        SKUL H 6 Bright
        SKUL I 6 Bright A_NoBlocking
        SKUL J 6
        SKUL K 6
        SOUL Z -1
        Stop
    Death.DRRPFireExtDmg:
        LSOL F 6 Bright A_Gravity
        LSOL G 5 Bright A_Scream
        LSOL H 5 Bright A_NoBlocking
        LSOL I 5 Bright
        SOUL Z -1
        Stop    
    }
}

Actor Demon_BaseDRRP: Demon {
    +LOOKALLAROUND

    DamageFactor DRRPRocketDmg, 0.5

    Radius 20
    Speed 5 // 10
}

Actor Cacodemon_BaseDRRP: Cacodemon {
    +LOOKALLAROUND

    DamageFactor DRRPAxeDmg, 10.0
    DamageFactor DRRPBulletDmg, 2.0
}

Actor Revenant_BaseDRRP: Revenant {
    Scale 0.74
    +LOOKALLAROUND

    DamageFactor DRRPAxeDmg, 5.0
    DamageFactor DRRPRocketDmg, 2.0

    States {
    Missile:
        SKEL J 0 Bright A_FaceTarget
        SKEL J 10 Bright A_FaceTarget
        SKEL K 10 A_SpawnProjectile( "DRRPRevenantTracer" )
        SKEL K 10 A_FaceTarget
        Goto See
    Death:
        SKEL L 7
        SKEL M 7 A_SpawnItem( "DRRPExplosion", 0, 38 )
        SKEL M 0 A_Jump( 256, "DeathReal" ) // For final monster cast.
        SKEL N 7 A_Scream
        SKEL OP 7
        SKEL Q -1
        Stop
    DeathReal:
        TNT1 A 7 A_Scream
        TNT1 A 7 A_NoBlocking
        TNT1 A 7
        TNT1 A -1
        Stop
    }
}

Actor PainShot : FatShot {
    Damage 2
}

Actor PainElemental_BaseDRRP: PainElemental_ZSPatch {
    +LOOKALLAROUND

    const int baseDamage = 10;

    DamageFactor DRRPFireDmg, 0.0
    DamageFactor DRRPPhysicalDmg, 2.0

    States {
    Missile:
        PAIN D 5 A_FaceTarget
        PAIN E 5 A_FaceTarget
        PAIN F 5 Bright A_FaceTarget
        PAIN F 0 Bright A_CustomComboAttack("PainShot", 32, 2 * Random(1, 5))
        Goto See
    }
}

Actor Fatso_BaseDRRP: Fatso {
    Scale 0.92
    Radius 30
    Height 52
    +LOOKALLAROUND

    DamageFactor DRRPRocketDmg, 2.5

    States {
    Missile:
        FATT G 20 A_FatRaise
        FATT H 10 Bright A_FatAttack1( "DRRPFatShot" )
        FATT IG 5 A_FaceTarget
        FATT H 10 Bright A_FatAttack2( "DRRPFatShot" )
        FATT IG 5 A_FaceTarget
        FATT H 10 Bright A_FatAttack3( "DRRPFatShot" )
        FATT IG 5 A_FaceTarget
        Goto See
    }
}

Actor ArchVile_BaseDRRP: ArchVile {
    const int vileInitialDamage = 10;
    const int vileBlastDamageNRadius = 45;

    Scale 0.7
    Speed 7 // Doom's default: 15.
    +LOOKALLAROUND

    DamageFactor DRRPAxeDmg, 10.0
    DamageFactor DRRPFireExtDmg, 4.0
    DamageFactor DRRPFireDmg, 0.0
    
    States {
    
    See:
        TNT1 A 0 Bright A_JumpIf(CheckClass("DRRPFire", AAPTR_TARGET, true), "Spawn") // Reset target if it's fire.
        VILE AABB 2 A_Chase
        VILE CC 2 A_VileChase
        VILE DDEE 2 A_Chase
        VILE FF 2 A_VileChase
        Loop
    Missile:
        VILE G 0 Bright A_VileStart
        VILE G 10 Bright A_FaceTarget
        VILE H 8 Bright A_VileTarget
        VILE IJKLMN 8 Bright A_FaceTarget
        VILE O 8 Bright A_VileAttack( "vile/stop", vileInitialDamage, vileBlastDamageNRadius, vileBlastDamageNRadius, 0.2 )
        VILE P 20 Bright
        Goto See
    }
}

Actor BaronOfHell_BaseDRRP: BaronOfHell {
    Scale 0.85
    +LOOKALLAROUND

    DamageFactor DRRPShotgunDmg, 5.0

    States {
    Melee:
    Missile:
        BOSS EF 8 A_FaceTarget
        BOSS G 8 A_CustomComboAttack( "DRRPBaronBall", 32, 10 * Random( 4, 9 ), "baron/melee" )
        Goto See
    }
}

Actor HellKnight_BaseDRRP: HellKnight {
    Scale 0.85
    +LOOKALLAROUND

    DamageFactor DRRPShotgunDmg, 5.0
}


// The cyberdemon and the Kronos doesn't need to be here because of their unique characteristics.
