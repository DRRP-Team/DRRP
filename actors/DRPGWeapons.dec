/**
 * Copyright (c) 2017-2019 DRRP-Team
 * 
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

//10556-10600

Actor DRRPAxe: DRRPAxe_zspatch 10557  {
    //$Category DWeapons

    +NOALERT
    +NOAUTOFIRE
    +WEAPON.MELEEWEAPON

    Tag "$DRRP_W_AXE"
    Scale 0.65
    Obituary "%o was chopped by %k"
    SeeSound "weapons/plasmax"

    Inventory.PickupMessage "$DRRP_W_AXEPICKUP"
    Inventory.Icon "AXEIA0"
    Inventory.PickupSound "TAUNT"
    Weapon.SelectionOrder 1880
    DamageType DRRPAxeDmg

    States {
        Spawn:
            AXEI A -1
            Stop
        Ready:
            AXEF A 1 A_WeaponReady
            Loop
        Select:
            AXEF A 1 A_Raise
            Loop
        Deselect:
            AXEF A 1 A_Lower
            Loop
        Fire:
            AXEF B 3
            AXEF C 3
            AXEF D 3
            AXEF D 1 A_CustomPunch(Random(3, 12), TRUE, 0, "DRRPAxePuff", 80, 0, 0, "ArmorBonus")
            AXEF E 5
            Goto Ready
    }
}

Actor DRRPAxePuff : BulletPuff {
    SeeSound "DSBLUD03"
    AttackSound "ROCK"    
}

Actor DRRPFireExt : DRRPFireExt_zspatch 10556 {
    //$Category DWeapons

    Tag "$DRRP_W_FIREEXT"
    Obituary "%o was extinguished by %k"
    Scale 0.5

    Inventory.Icon "FEXTI0"
    Inventory.PickupSound "voice/wtf"
    Inventory.Pickupmessage "$DRRP_W_FIREEXTPICKUP"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 40
    Weapon.AmmoType "Balloon"
    Weapon.SelectionOrder 100
    DamageType DRRPFireExtDmg

    States {
        Ready: // Original sprite -- "DW2A"
            FIEX A 1 A_WeaponReady
            Loop
        Deselect:
            FIEX B 0 ACS_NamedExecuteAlways("fireextfade", 0, 0)
            FIEX B 0 A_StopSound(CHAN_WEAPON)
            FIEX A 1 A_Lower
            Loop
        Select:
            FIEX A 1 A_Raise
            Loop
        Fire:
            FIEX B 0 A_Recoil(1.7)
            TNT1 A 0 A_PlaySound("weapon/fireext/loop", CHAN_WEAPON, 1, 1)
            FIEX B 1 A_FireCustomMissile("FireExtPuff", Random(0, 3/*10*/), True)
            FIEX B 0 A_Recoil(0.7)
            FIEX BB 1 A_FireCustomMissile("FireExtPuffNoDamage" , Random(0, 10), False)
            FIEX B 0 ACS_NamedExecuteAlways("fireextfade", 0, 1)
        FireLoop:
            //TNT1 A 0 A_Quake(1, Random(4, 8), 0, 4)
            TNT1 A 0 A_PlaySound("weapon/fireext/loop", CHAN_WEAPON, 1, 1)
            TNT1 A 0 A_SetAngle(FRandom(0.2, -0.2) + Angle)
            TNT1 A 0 A_SetPitch(FRandom(0.2, -0.5) + Pitch)
            FIEX C 0 A_Recoil(0.01)
            FIEX C 1 A_FireCustomMissile("FireExtPuff", Random(0, 10), True)
            TNT1 A 0 A_SetBlend("CC CC CC", FRandom(0.795, 0.80), Random(10, 20))
            TNT1 A 0 A_SetAngle(FRandom(0.2, -0.2) + Angle)
            TNT1 A 0 A_SetPitch(FRandom(0.2, -0.5) + Pitch)
            FIEX C 0 A_Recoil(0.02)
            FIEX CC 1 A_FireCustomMissile("FireExtPuffNoDamage" , Random(0, 10), False)
            TNT1 A 0 A_SetAngle(FRandom(0.2, -0.2) + Angle)
            TNT1 A 0 A_SetPitch(FRandom(0.2, -0.5) + Pitch)
            FIEX C 1 A_FireCustomMissile("FireExtPuff", Random(0, 10), True)
            TNT1 A 0 A_SetAngle(FRandom(0.2, -0.2) + Angle)
            TNT1 A 0 A_SetPitch(FRandom(0.2, -0.5) + Pitch)
            FIEX C 0 A_Recoil(0.02)
            FIEX CC 1 A_FireCustomMissile("FireExtPuffNoDamage" , Random(0, 10), False)
            TNT1 A 0 A_SetAngle(FRandom(0.2, -0.2) + Angle)
            TNT1 A 0 A_SetPitch(FRandom(0.2, -0.5) + Pitch)
            FIEX A 0 A_ReFire("FireLoop")
            FIEX B 1 A_FireCustomMissile("FireExtPuff", Random(0, 10), True)
            FIEX BB 1 A_FireCustomMissile("FireExtPuffNoDamage" , Random(0, 10), False)
            FIEX B 0 A_SetAngle(FRandom(1, -1) + Angle)
            TNT1 A 0 A_SetPitch(FRandom(1, -1) + Pitch)
            FIEX A 0 A_ReFire
            TNT1 A 0 A_StopSound(CHAN_WEAPON)
            TNT1 A 0 A_PlaySound("weapon/fireext/stop", CHAN_WEAPON)
            FIEX B 0 ACS_NamedExecuteAlways("fireextfade", 0, 0)
            Goto Ready
        Spawn:
            FEXT I -1
            Stop
    }
}

Actor FireExtPuff {
    PROJECTILE
    +FORCEXYBILLBOARD
    +SPECTRAL

    Height 5
    Radius 5
    Alpha 0.85
    Speed 30
    Scale 0.5
    Gravity 10
    Damage (1)
    DamageType DRRPFireExtDmg
    RenderStyle Add

    States {
        Spawn:
            STEM AAAAA 0 A_SpawnParticle(Gray, RandomPick(0, SPF_FULLBRIGHT), Random(3, 35), FRandom(0.75, 4), 0, 
                            Random(-5, 5), Random(-5, 5), Random(-5, 5), // Offsets
                            Random(-10, 10), Random(-10, 10), Random(-10, 10), // Speed
                            Random(-10, 10), Random(-10, 10), Random(-10, 10)) // Acceleration
            STEM A 1 A_SetTranslucent(0.7, 1)
            STEM B 1 A_SetScale(1.3, 1.3)//A_SetTranslucent(0.65, 1)
            STEM C 1 A_SetTranslucent(0.6, 1)
            STEM D 1 A_SetScale(1.5, 1.5)//A_SetTranslucent(0.55, 1)
            STEM E 1 A_SetTranslucent(0.5, 1)
            STEM F 1 A_SetScale(1.8, 1.8)//A_SetTranslucent(0.45, 1)
            STEM G 1 A_SetTranslucent(0.4, 1)
            STEM H 1 A_SetScale(2.0, 2.0)//A_SetTranslucent(0.35, 1)
            STEM I 1 A_SetTranslucent(0.3, 1)
            STEM J 1 A_SetTranslucent(0.2, 1)
            STEM K 1 A_SetTranslucent(0.1, 1)
            STEM L 1 A_SetTranslucent(0.05, 1)
        Death:
            STEM LLLLLLLLL 0 A_SpawnParticle(Gray, 0, Random(5, 70), FRandom(0.5, 2), 0, 
                            0, Random(-5, 0), Random(-5, 5), // Offsets
                            Random(-10, 10), Random(-10, 10), Random(-10, 10), // Speed
                            Random(-5, 5), Random(-5, 5), Random(-5, 5)) // Acceleration
            Stop
    }
}

Actor FireExtPuffNoDamage: FireExtPuff {
    Damage 0
}

// Dogs

Actor HellHoundGun : Weapon {
    +WEAPON.WIMPY_WEAPON

    Tag "$DRRP_W_HELLHOUND"
    Inventory.PickupMessage "$DRRP_W_HELLHOUNDPICKUP"
    DamageType DRRPPhysicalDmg

    Weapon.AmmoUse 0
    Weapon.AmmoGive 100
    Weapon.SelectionOrder 1
    //Weapon.AmmoType "DogGunHealth"
    Inventory.Pickupmessage "DOGGY!"
    Inventory.PickupSound "weapons/fireext/pick"

    States {
        Ready:
            HDGG AAAAAAAA 1 A_WeaponReady
            Loop
        Select:
            HDGG A 1 Offset(0, 90)
            HDGG A 0 A_PlaySound("monsters/hellhound/see")
            HDGG A 1 Offset(0, 80)
            HDGG A 1 Offset(0, 70)
            HDGG A 1 Offset(0, 60)
            HDGG A 1 Offset(0, 55)
            HDGG A 1 Offset(0, 49)
            HDGG A 1 Offset(0, 44)
            HDGG A 1 Offset(0, 39)
            HDGG A 1 Offset(0, 35)
            HDGG A 1 Offset(0, 33)
            HDGG A 1 Offset(0, 32) 
            Goto Ready
        Deselect:
            HDGG A 1 A_Lower
            Loop
        Fire:
            TNT1 A 0 A_GunFlash("Flash", GFF_NOEXTCHANGE)
            HDGG A 0 A_PlaySound("monsters/hellhound/bark")
            HDGG CD 2
            HDGG E 2 A_CustomPunch(random(6, 12), TRUE, 0, "DogGunPuff", 80, 0, 0, "ArmorBonus", "monsters/hellhound/bark")
            HDGG F 2 
            HDGG DCB 2
            Goto Ready
        Flash:
            TNT1 A 1 A_SetPitch(Pitch + Random(2, 4))
            TNT1 A 0 A_SetAngle(Angle + FRandom(-1, 1))
            TNT1 A 1 A_SetPitch(Pitch + Random(2, 5))
            TNT1 A 1 A_SetPitch(Pitch + Random(1, 4))
            TNT1 A 1 A_Recoil(Random(-7, -4))
            TNT1 A 0 A_SetAngle(Angle + FRandom(-1, 1))
            TNT1 A 1 A_SetPitch(Pitch - FRandom(5, 8))
            TNT1 A 0 A_SetAngle(Angle + FRandom(-2, 2))
            TNT1 A 1 A_SetPitch(Pitch - FRandom(3, 4))
            TNT1 A 0 A_SetAngle(Angle + FRandom(-2, 2))
            TNT1 A 1 A_SetPitch(Pitch - FRandom(3, 5))
            TNT1 A 0 A_SetAngle(Angle + FRandom(-3, 3))
            TNT1 A 1 A_SetPitch(Pitch - 4)
            TNT1 A 2
            TNT1 A 1 A_SetPitch(Pitch + 3)
            Stop
        Spawn:
            TNT1 A 1
            Stop
    }
}

Actor CerberusGun : HellHoundGun {
    Tag "$DRRP_W_CERBERUS"
    Inventory.PickupMessage "$DRRP_W_CEREBRUSPICKUP"

    States {
        Ready:
            CDGG AAAAAAAA 1 A_WeaponReady
            Loop
        Select:
            CDGG A 1 Offset(0, 90)
            CDGG A 0 A_PlaySound("monsters/cerberus/see")
            CDGG A 1 Offset(0, 80)
            CDGG A 1 Offset(0, 70)
            CDGG A 1 Offset(0, 60)
            CDGG A 1 Offset(0, 55)
            CDGG A 1 Offset(0, 49)
            CDGG A 1 Offset(0, 44)
            CDGG A 1 Offset(0, 39)
            CDGG A 1 Offset(0, 35)
            CDGG A 1 Offset(0, 33)
            CDGG A 1 Offset(0, 32) 
            GoTo Ready
        Deselect:
            CDGG A 1 A_Lower
            Loop
        Fire:
            TNT1 A 0 A_GunFlash("Flash", GFF_NOEXTCHANGE)
            CDGG A 0 A_PlaySound("monsters/cerberus/bark")
            CDGG CD 2
            CDGG E 2 A_CustomPunch(random(7, 20), TRUE, 0, "DogGunPuff", 80, 0, 0, "ArmorBonus", "monsters/cerberus/bark")
            CDGG F 2 
            CDGG DCB 2
            Goto Ready
         Spawn:
            TNT1 A 1
            Stop
    }
}

Actor DemonWolfGun : HellHoundGun {
    Tag "$DRRP_W_DEMONWOLF"
    Inventory.PickupMessage "$DRRP_W_DEMONWOLFPICKUP"

    States {
        Ready:
            DDGG AAAAAAAA 1 A_WeaponReady
            Loop
        Select:
            DDGG A 1 Offset(0, 90)
            DDGG A 0 A_PlaySound("monsters/demonwolf/see")
            DDGG A 1 Offset(0, 80)
            DDGG A 1 Offset(0, 70)
            DDGG A 1 Offset(0, 60)
            DDGG A 1 Offset(0, 55)
            DDGG A 1 Offset(0, 49)
            DDGG A 1 Offset(0, 44)
            DDGG A 1 Offset(0, 39)
            DDGG A 1 Offset(0, 35)
            DDGG A 1 Offset(0, 33)
            DDGG A 1 Offset(0, 32) 
            GoTo Ready
        Deselect:
            DDGG A 1 A_Lower
            Loop
        Fire:
            TNT1 A 0 A_GunFlash("Flash", GFF_NOEXTCHANGE)
            DDGG A 0 A_PlaySound("monsters/demonwolf/bark")
            DDGG CD 2
            DDGG E 2 A_CustomPunch(random(10, 35), TRUE, 0, "DogGunPuff", 80, 0, 0, "ArmorBonus", "monsters/demonwolf/bark")
            DDGG F 2 
            DDGG DCB 2
            Goto Ready
        Spawn:
            TNT1 A 1
            Stop
    }
}

Actor DogGunPuff {
    +NOBLOCKMAP
    +NOGRAVITY
    +ALLOWPARTICLES
    +RANDOMIZE

    Alpha 0.5
    VSpeed 1
    Mass 5
    AttackSound "weapons/melee/hitwall"
    SeeSound "weapons/melee/hit"
    Obituary "$OB_METADOG_BITE"
    DamageType DRRPPhysicalDmg
    RenderStyle Translucent

    States {
        Spawn:
            PUFF A 4 Bright
            PUFF B 4
        Melee:
            PUFF CD 4
            Stop
    }
}
