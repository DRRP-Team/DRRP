/**
 * Copyright (c) 2017-2018 DRRP-Team
 * 
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

//Mask: 10 + <monster_skill + 1> + <ID>: e. g., 2 monster skill = 10300-10399.
//So, Monsters: 10259-10499.

//Dogs
    //Hellhound     10246
    //Cerberus      10346
    //DemonWolf     10446
//Zombies
    //ZombieManDRRP <same as original>
    //ShotgunGuyDRRP    <same as original>
    //ZombiePvt     10274
    //ZombieLt      10374
    //ZombieCpt     10474
//Chaingunners
    //ChaingunGuyDRRP   <same as original>
    //Troop         10264
    //Commando      10364
    //Assassin      10464
//Imps
    //Impling       10260
    //Imp           10360
    //ImpLord       10460
//Lost souls
    //Phantom       10263
    //LostSoul1     10463 FIXME:
    //Nightmare     10363
//Demons
    //BullDemon     10259
    //Pinky         10459 FIXME:
    //Belphegor     10359
//Cacodemons
    //Malrath       10262
    //Cacodemon1    10362
    //Wretched      10462
//Revenants
    //Ghoul         10277
    //Fiend         10377
    //RevenantDRRP  10477
//Pain Elementals
    //Rahovart      10379
    //Beholder      10479
    //DRRPPainElemental 10279 FIXME:
//Mancubuses
    //Behemoth      10278
    //Mancubus      10378
    //Druj          10478
//Viles
    //Infernis      10273
    //ArchVile1     10373
    //Apollyon      10473
//BaronsOfHellKnights
    //Ogre          10480 FIXME:
    //HellKnight1   10380 FIXME:
    //BaronOfHell   10280

//Bosses
    //Kronos TODO:
    //Cyberdemon TODO:



// Dogs -------------------------------------------------------------------------------

Actor DRRPDog : MBFHelperDog {
    +LOOKALLAROUND

    Health       8
    DamageFactor DRRPShotgunDmg, 2.0
    DamageFactor DRRPPlasmaDmg, 2.0

    ActiveSound  "monsters/hellhound/see"
    PainSound    "monsters/hellhound/pain"
    DeathSound   "monsters/hellhound/death"
    SeeSound     "monsters/hellhound/bark"
    AttackSound  "monsters/hellhound/bark"
}

Actor Hellhound : DRRPDog replaces MBFHelperDog 10246 {
    //$Category DMonsters
    //$Sprite DOGSA1

    Tag "$DRRP_M_HELLHOUND"
    Health 8
}

Actor Cerberus : DRRPDog 10346 {
    //$Category DMonsters
    //$Sprite DOGSA1

    Tag "$DRRP_M_CERBERUS"
    Health 12
    Translation "64:79=32:47"

    ActiveSound  "monsters/cerberus/see"
    SeeSound     "monsters/cerberus/bark"
    AttackSound  "monsters/cerberus/bark"
}

Actor DemonWolf : DRRPDog 10446 {
    //$Category DMonsters
    //$Sprite DOGSA1

    Tag "$DRRP_M_DEMONWOLF"
    Health 17
    Translation "64:79=197:200"

    ActiveSound "monsters/demonwolf/see"
    SeeSound    "monsters/demonwolf/bark"
    AttackSound "monsters/demonwolf/bark"
}


// Zombies ----------------------------------------------------------------------------

Actor ZombieManDRRP : ZombieMan {
    +LOOKALLAROUND

    DropItem "Clip", 0, 0
    DamageFactor DRRPAxeDmg, 2.0
}

Actor ShotgunGuyDRRP : ShotgunGuy {
    +LOOKALLAROUND

    DropItem "Shells", 0, 0
    DamageFactor DRRPAxeDmg, 2.0
}

Actor ZombiePvt : ZombieManDRRP replaces ZombieMan 10274 {
    //$Category DMonsters
    //$Sprite POSSA1

    Tag "$DRRP_M_ZOMBIEPVT"
    Health 6
    Translation "16:47=152:159"
}

Actor ZombieLt : ShotgunGuyDRRP replaces ShotgunGuy 10374 {
    //$Category DMonsters
    //$Sprite POSSA1

    Tag "$DRRP_M_ZOMBIELT"
    Health 9
    Translation "96:111=192:207", "0:8=246:246" 
}

Actor ZombieCpt : ZombieManDRRP 10474 {
    //$Category DMonsters
    //$Sprite POSSA1

    Tag "$DRRP_M_ZOMBIECPT"
    Health 12
    Translation "128:143=176:189", "144:151=178:191", "16:47=180:191", "152:159=180:191", "112:127=176:191"
}


// Chaingunners -----------------------------------------------------------------------

Actor ChaingunGuyDRRP : ChaingunGuy {
    +LOOKALLAROUND

    DropItem "Chaingun", 0, 0
    DamageFactor DRRPPlasmaDmg, 2.0

    States {
        Pain:
            CPOS G 0 A_Pain
            CPOS G 4 A_MonsterRefire(40, "PainOut")
            Goto Missile + 1
        PainOut:
            CPOS G 2
            Goto See
    }
}

Actor Troop : ChaingunGuyDRRP 10264 {
    //$Category DMonsters
    //$Sprite CPOSA1

    Tag "$DRRP_M_TROOP"
    Health 11
    Translation "32:47=163:167"
}

Actor Commando : ChaingunGuyDRRP 10364 {
    //$Category DMonsters
    //$Sprite CPOSA1

    Tag "$DRRP_M_COMMANDO"
    Health 18
    Translation "16:47=192:207"
}

Actor Assassin : ChaingunGuyDRRP replaces ChaingunGuy 10464 {
    //$Category DMonsters
    //$Sprite CPOSA1

    Tag "$DRRP_M_ASSASSIN"
    Health 25
}


// Imps -------------------------------------------------------------------------------

Actor Impling : DoomImp 10260 {
    //$Category DMonsters
    //$Sprite TROOA1

    +LOOKALLAROUND

    Tag "$DRRP_M_IMPLING"
    Health 10
    Translation "64:79=112:127", "61:63=112:118"
}

Actor DRRPImp : DoomImp replaces DoomImp 10360 {
    //$Category DMonsters
    //$Sprite TROOA1

    +LOOKALLAROUND

    Tag "$DRRP_M_IMP"
    Health 16
}

Actor ImpLord : DoomImp 10460 {
    //$Category DMonsters
    //$Sprite TROOA1

    +LOOKALLAROUND

    Tag "$DRRP_M_IMPLORD"
    Health 22
    Translation "64:79=170:191"
}


// Lost souls -------------------------------------------------------------------------

Actor DRRPLostSoul : LostSoul replaces LostSoul 10363 {
    //$Category DMonsters
    //$Sprite SKULA1

    +LOOKALLAROUND
	-DONTFALL

    Tag "$DRRP_M_LOSTSOUL"
    Health 11
    DamageFactor DRRPFireExtDmg, 5.0
    DamageFactor DRRPFireDmg, 0.0

    States {
		See:
			TNT1 A 0 Bright A_JumpIf(CheckClass("DRRPFire", AAPTR_TARGET, true), "Spawn") // Reset target if it's fire
			SKUL AB 6 Bright A_Chase
		Loop
        Death:
            SKUL F 6 Bright
            SKUL G 6 Bright A_Scream
            SKUL H 6 Bright
            SKUL I 6 Bright A_NoBlocking
            SKUL J 6
            SKUL K 6
            SOUL Z -1
            Stop
        Death.DRRPFireExtDmg:
            LSOL F 6 Bright A_Gravity
            LSOL G 5 Bright A_Scream
            LSOL H 5 Bright A_NoBlocking
            LSOL I 5 Bright
            SOUL Z -1
            Stop    
    }
}

Actor Phantom : DRRPLostSoul 10263 {
    //$Category DMonsters
    //$Sprite SKULA1

    Tag "$DRRP_M_PHANTOM"
    Health 7
    Translation "128:151=152:159", "64:79=152:159"
}

Actor Nightmare : DRRPLostSoul 10463 {
    //$Category DMonsters
    //$Sprite SKULA1

    Tag "$DRRP_M_NIGHTMARE"
    Health 15
    Translation "128:151=195:207", "64:79=200:207"
}


// Demons -----------------------------------------------------------------------------

Actor BullDemon : Demon 10259 {
    //$Category DMonsters
    //$Sprite SARGA1

    +LOOKALLAROUND

    Tag "$DRRP_M_BULLDEMON"
    Health 11
    Translation "16:47=128:143", "168:191=144:151"
}

Actor Pinky : Demon replaces Demon 10459 { //FIXME: id
    //$Category DMonsters
    //$Sprite SARGA1

    +LOOKALLAROUND

    Tag "$DRRP_M_PINKY"
    Health 18
}

Actor Belphegor : Demon 10359 {
    //$Category DMonsters
    //$Sprite SARGA1

    +LOOKALLAROUND

    Tag "$DRRP_M_BELPHEGOR"
    Health 25
    Translation "16:47=%[0.00,0.09,0.20]:[0.49,1.17,2.00]"
}


// Cacodemons -------------------------------------------------------------------------

Actor Malwrath : Cacodemon 10262 {
    //$Category DMonsters
    //$Sprite HEADA1

    +LOOKALLAROUND

    Tag "$DRRP_M_MALWRATH"
    Health 15
    Translation "168:191=160:167", "16:47=160:166", "112:127=176:191", "192:207=176:191", "240:247=191:191" //"173:191=[215,222,0]:[107,65,0]", "16:47=[214,190,123]:[107,81,0]", "167:167=164:164", "192:207=32:39", "240:247=40:47", "112:127=16:47"
}

Actor DRRPCacodemon : Cacodemon replaces Cacodemon 10362 {
    //$Category DMonsters
    //$Sprite HEADA1

    +LOOKALLAROUND

    Tag "$DRRP_M_CACODEMON"
    Health 23
}

Actor Wretched : Cacodemon 10462 {
    //$Category DMonsters
    //$Sprite HEADA1

    +LOOKALLAROUND

    Tag "$DRRP_M_WRETCHED"
    Health 32
    Translation "168:191=193:207", "16:47=193:201", "112:127=160:167", "192:207=168:191", "160:167=207:207", "223:223=207:207"
}


// Revenants --------------------------------------------------------------------------

Actor DRRPRevenant : Revenant replaces Revenant 10477 { // Huesos (Spanish)
    //$Category DMonsters
    //$Sprite SKELA1D1

    +LOOKALLAROUND

    Tag "$DRRP_M_REVENANT"
    Health 32
    Scale 0.74
    Obituary "%o was slayed by the revenant."

    States {
        Death:
            SKEL L 7 A_SpawnItem("RocketAmmo")
            SKEL M 7 A_SpawnItem("Explosion", 0, 38.)
            SKEL N 7 A_Scream
            SKEL O 7 A_NoBlocking
            SKEL P 7
            SKEL Q -1
            Stop
    }
}

Actor Ghoul : DRRPRevenant 10277 {
    //$Category DMonsters
    //$Sprite SKELA1D1

    Tag "$DRRP_M_GHOUL"
    Health 15
    Translation "128:143=112:127", "144:151=120:127", "48:79=112:127", "160:167=114:126", "13:15=126:127", "232:235=124:124", "20:20=116:116", "236:239=125:127", "21:47=160:16"
}

Actor Fiend : DRRPRevenant 10377 {
    //$Category DMonsters
    //$Sprite SKELA1D1

    Tag "$DRRP_M_FIEND"
    Health 23
    Translation "128:159=192:207", "48:79=192:207", "160:167=240:240", "236:239=243:243", "170:170=240:240", "21:47=176:191", "20:20=207:207", "168:191=176:191", "184:184=242:242", "9:9=207:207", "144:151=192:207", "13:15=241:241"
}


// Pain Elementals --------------------------------------------------------------------

Actor DRRPPainElemental : PainElemental replaces PainElemental 10279 {
    //$Category DMonsters
    //$Sprite PAINA1

    +LOOKALLAROUND

    Tag "$DRRP_M_PAINELEMENTAL"
    Health 37
    DamageFactor DRRPFireDmg, 0.0

    States {
        Missile:
            PAIN D 5 A_FaceTarget
            PAIN E 5 A_FaceTarget
            PAIN F 5 Bright A_FaceTarget
            PAIN F 0 Bright A_CustomComboAttack("FatShot", 32, 2 * random(1, 6))
            Goto See
        Death:
            PAIN H  8 Bright
            PAIN I  8 Bright A_Scream
            PAIN JK 8 Bright
            PAIN L  8 Bright A_PainDie("Nightmare")
            PAIN M  8 Bright
            PAIN Z -1
            Stop
    }
}

Actor Rahovart : DRRPPainElemental 10379 {
    //$Category DMonsters
    //$Sprite PAINA1

    Tag "$DRRP_M_RAHOVART"
    Health 27
    Translation "13:15=204:206", "64:70=200:206", "128:143=192:207", "236:239=240:247", "71:79=246:246"

    States {
        Death:
            PAIN H  8 Bright
            PAIN I  8 Bright A_Scream
            PAIN JK 8 Bright
            PAIN L  8 Bright A_PainDie("DRRPLostSoul")
            PAIN M  8 Bright
            PAIN Z -1
            Stop
    }
}

Actor Beholder : DRRPPainElemental 10479 {
    //$Category DMonsters
    //$Sprite PAINA1

    Tag "$DRRP_M_BEHOLDER"
    Health 17
    Translation "13:15=124:127", "64:70=120:127", "128:143=112:127", "236:239=122:127", "71:79=127:127"

    States {
        Death:
            PAIN H  8 Bright
            PAIN I  8 Bright A_Scream
            PAIN JK 8 Bright
            PAIN L  8 Bright A_PainDie("Phantom")
            PAIN M  8 Bright
            PAIN Z -1
            Stop            
    }
}


// Mancubuses -------------------------------------------------------------------------
// Dull joke by UsernameAK: "Minecraft -- cube world".
// - PROPHESSOR laughs

Actor DRRPCubus : Fatso {
    +LOOKALLAROUND

    Scale 0.92
    Radius 30
    Height 62
    DamageFactor DRRPRocketDmg, 2.0
}

Actor Behemoth : DRRPCubus 10278 {
    //$Category DMonsters
    //$Sprite FATTA1

    Tag "$DRRP_M_BEHEMOTH"
    Health 23
    Translation "48:79=192:202", "112:127=176:191", "128:143=192:198", "160:167=194:199", "144:151=192:207", "13:15=240:247", "236:239=240:247", "16:47=176:191"
}

Actor Mancubus : DRRPCubus replaces Fatso 10378 {
    //$Category DMonsters
    //$Sprite FATTA1

    Tag "$DRRP_M_MANCUBUS"
    Health 36
}

Actor Druj : DRRPCubus 10478 {
    //$Category DMonsters
    //$Sprite FATTA1

    Tag "$DRRP_M_DRUJ"
    Translation "48:79=16:45", "144:151=16:45", "144:151=16:45", "236:239=46:46", "13:15=46:46", "141:141=46:46", "138:138=46:46", "142:142=45:45", "143:143=45:45", "16:47=176:189"
}


// Arch-Viles -------------------------------------------------------------------------

Actor DRRPVile : ArchVile {
    +LOOKALLAROUND

    Scale 0.7
    DamageFactor DRRPAxeDmg, 8.0
    DamageFactor DRRPFireDmg, 0.0
	
	States {
		
		See:
			TNT1 A 0 Bright A_JumpIf(CheckClass("DRRPFire", AAPTR_TARGET, true), "Spawn") // Reset target if it's fire
			VILE AABBCCDDEEFF 2 A_VileChase
		Loop
	}
}

Actor Infernis : DRRPVile 10273 {
    //$Category DMonsters
    //$Sprite VILEA1D1

    Tag "$DRRP_M_INFERNIS"
    Health 20
    Translation  "48:79=192:207", "128:143=192:207", "13:15=240:247", "144:151=240:247", "236:239=240:247", "144:151=240:247", "208:223=192:207", "0:2=247:247"
}

Actor DRRPArchVile : DRRPVile replaces ArchVile 10373 {
    //$Category DMonsters
    //$Sprite VILEA1D1

    Tag "$DRRP_M_ARCHVILE"
    Health 32
}

Actor Apollyon : DRRPVile 10473 {
    //$Category DMonsters
    //$Sprite VILEA1D1

    Tag "$DRRP_M_APOLLYON"
    Health 45
    Translation "64:79=32:47", "48:63=16:31", "128:143=16:39", "236:239=44:47", "13:15=45:47", "144:151=32:39" // "16:223=%[0.00,0.00,0.00]:[2.00,0.78,0.78]"
}


// Barons of Hell ---------------------------------------------------------------------

Actor DRRPBaronOfHell : BaronOfHell replaces BaronOfHell 10280 {
    //$Category DMonsters
    //$Sprite BOSSA1

    +LOOKALLAROUND

    Tag "$DRRP_M_BARON"
    Scale 0.85
    Health 62
    DamageFactor DRRPSSGDmg, 5.0
}

Actor DRRPHellKnight : HellKnight replaces HellKnight 10380 {
    //$Category DMonsters
    //$Sprite BOS2A1

    +LOOKALLAROUND

    Tag "$DRRP_M_HELLKNIGHT"
    Scale 0.85
    Health 45
    DamageFactor DRRPSSGDmg, 5.0
    //Translation Baron:HellKnight
}

Actor Ogre : DRRPHellKnight 10480 {
    //$Category DMonsters
    //$Sprite BOS2A1

    Tag "$DRRP_M_OGRE"
    Health 28
    Translation "144:151=123:127", "64:79=122:127", "55:63=116:126", "48:55=112:116", "128:143=121:127", "13:15=127:127"
}

// TODO:
Actor DRRPCyberdemon : Cyberdemon replaces Cyberdemon 10390 {
    //$Category DMonsters
    //$Sprite CYBRA1

    +LOOKALLAROUND

    Tag "$DRRP_M_CYBERDEMON"
    Health 687
}

Actor Kronos 10391 {
    //$Category DMonsters
    //$Sprite KRONA1

	//const int KronosScale = 0.43;

    MONSTER
    +LOOKALLAROUND
    +BOSS

    Radius  24
    Height  56
    Health  458 // Original: 344..572
    Speed   14
    Scale   0.43
    Tag     "$DRRP_M_KRONOS"

    //SeeSound "DRRPMonsters/Kronos/sight"
    //PainSound "DRRPMonsters/Kronos/pain"
    //DeathSound "DRRPMonsters/Kronos/death"
    //ActiveSound "DRRPMonsters/Kronos/active"

    States {
        Spawn:
            KRON A 5 A_Look
            Wait
        See:
            KRON AAA 3 A_Chase
			KRON A 0 A_SetScale( -0.43, 0.43 )
			KRON AAA 3 A_Chase
			KRON A 0 A_SetScale( 0.43, 0.43 )
            Loop
        Melee:
            KRON A 5
            KRON A 7 A_CustomMeleeAttack( Random( 15, 30 ) )
            Goto See
        Death:
            KRON M 3 {
                A_Fall;
                A_NoBlocking;
            }
            KRON NOP 2
			KRON QRST 3
			KRON UVWX 2
			KRON Y 15
			KRON Z -1
            Stop
    }    
}
