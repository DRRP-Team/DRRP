/**
 * Copyright (c) 2017-2019 DRRP-Team
 * 
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

//Mask: 10 + <monster_skill + 1> + <ID>: e. g., 2 monster skill = 10300-10399.
//So, Monsters: 10259-10499.


// Dogs -------------------------------------------------------------------------------

Actor DRRPDog : MBFHelperDog {
    +LOOKALLAROUND

    DamageFactor DRRPShotgunDmg, 2.0
    DamageFactor DRRPPlasmaDmg, 2.0

    ActiveSound  "monsters/hellhound/see"
    PainSound    "monsters/hellhound/pain"
    DeathSound   "monsters/hellhound/death"
    SeeSound     "monsters/hellhound/bark"
    AttackSound  "monsters/hellhound/bark"

    States {
    Melee:
        DOGS EF 8 A_FaceTarget
        DOGS G 8 A_CustomMeleeAttack( Random( 1, 5 ) * 3 + 2 )
        Goto See
    }
}

Actor Hellhound : DRRPDog replaces MBFHelperDog 10246 {
    //$Category DMonsters
    //$Sprite DOGSA1

    Health 40
    Tag "$DRRP_M_HELLHOUND"
    Obituary "$DRRP_OB_HELLHOUND"
}

Actor Cerberus : DRRPDog 10346 {
    //$Category DMonsters
    //$Sprite DOGSA1

    Tag "$DRRP_M_CERBERUS"
    Obituary "$DRRP_OB_CEREBRUS"
    Health 90
    Translation "64:79=32:47"

    ActiveSound  "monsters/cerberus/see"
    SeeSound     "monsters/cerberus/bark"
    AttackSound  "monsters/cerberus/bark"

    States {
    Melee:
        DOGS EF 8 A_FaceTarget
        DOGS G 8 A_CustomMeleeAttack( Random( 2, 5 ) * 3 + 3 )
        Goto See
    }
}

Actor DemonWolf : DRRPDog 10446 {
    //$Category DMonsters
    //$Sprite DOGSA1

    Tag "$DRRP_M_DEMONWOLF"
    Obituary "$DRRP_OB_DEMONWOLF"
    Health 190
    Translation "64:79=197:200"

    ActiveSound "monsters/demonwolf/see"
    SeeSound    "monsters/demonwolf/bark"
    AttackSound "monsters/demonwolf/bark"

    States {
    Melee:
        DOGS EF 8 A_FaceTarget
        DOGS G 8 A_CustomMeleeAttack( Random( 2, 5 ) * 4 + 3 )
        Goto See
    }
}


// Zombies ----------------------------------------------------------------------------

Actor ZombieManDRRP : ZombieMan {
    +LOOKALLAROUND

    DropItem "Clip", 0, 0
    DamageFactor DRRPAxeDmg, 3.0

    States {
    Missile:
        POSS E 10 A_FaceTarget
        POSS F 8 A_CustomBulletAttack( 22.5, 0, 1, Random( 1, 5 ) * 3, "BulletPuff", 0, CBAF_NORANDOM )
        POSS E 8
        Goto See
    }
}

Actor ShotgunGuyDRRP : ShotgunGuy {
    +LOOKALLAROUND

    DropItem "DRRPShell", 0, 0
    DamageFactor DRRPAxeDmg, 3.0

    States {
    Missile:
        SPOS E 10 A_FaceTarget
        SPOS F 10 A_CustomBulletAttack( 22.5, 0, 3, Random( 1, 5 ) * 3 - 1, "BulletPuff", 0, CBAF_NORANDOM )
        SPOS E 10
        Goto See
    }
}

Actor ZombiePvt : ZombieManDRRP replaces ZombieMan 10274 {
    //$Category DMonsters
    //$Sprite POSSA1

    Obituary "$DRRP_OB_ZOMBIEPVT"
    Tag "$DRRP_M_ZOMBIEPVT"
    Health 35
    Translation "16:47=152:159"

    States {
    Missile:
        POSS E 10 A_FaceTarget
        POSS F 8 A_CustomBulletAttack( 22.5, 0, 1, Random( 3, 6 ) * 3 + 2, "BulletPuff", 0, CBAF_NORANDOM )
        POSS E 8
        Goto See
    }
}

Actor ZombieLt : ShotgunGuyDRRP replaces ShotgunGuy 10374 {
    //$Category DMonsters
    //$Sprite POSSA1

    Obituary "$DRRP_OB_ZOMBIELT"
    Tag "$DRRP_M_ZOMBIELT"
    Health 75
    Translation "96:111=%[0.00,0.00,0.00]:[0.98,1.25,1.76]", "16:47=%[0.00,0.00,0.00]:[0.31,0.59,0.94]", "48:79=%[0.00,0.00,0.00]:[0.80,0.93,1.16]", "176:191=240:240", "168:175=193:193", "208:223=%[0.00,0.00,0.00]:[0.83,1.03,1.28]", "160:167=%[0.00,0.00,0.00]:[0.76,0.95,1.28]" 

    States {
    Missile:
        SPOS E 10 A_FaceTarget
        SPOS F 10 A_CustomBulletAttack( 22.5, 0, 3, Random( 2, 6 ) * 3 + 1, "BulletPuff", 0, CBAF_NORANDOM )
        SPOS E 10
        Goto See
    }
}

Actor ZombieCpt : ZombieManDRRP 10474 {
    //$Category DMonsters
    //$Sprite POSSA1

    Obituary "$DRRP_OB_ZOMBIECPT"
    Tag "$DRRP_M_ZOMBIECPT"
    Health 155
    Translation "100:107=%[0.00,0.00,0.00]:[1.76,0.16,0.00]", "26:47=%[0.00,0.00,0.00]:[1.02,0.07,0.00]", "48:79=%[0.00,0.00,0.00]:[1.17,0.87,0.80]", "176:191=185:185", "168:175=173:173", "208:223=%[0.00,0.00,0.00]:[0.83,1.03,1.28]", "160:167=%[0.00,0.00,0.00]:[1.53,0.02,0.06]"

    States {
    Missile:
        POSS E 10 A_FaceTarget
        POSS E 0 A_FaceTarget
        POSS FFF 3 A_CustomBulletAttack( 22.5, 0, 1, Random( 2, 6 ) * 4 + 2, "BulletPuff", 0, CBAF_NORANDOM )
        POSS E 8
        POSS E 8 A_Jump( 195, "See" )
        Loop
    }
}


// Chaingunners -----------------------------------------------------------------------

Actor ChaingunGuyDRRP : ChaingunGuy {
    +LOOKALLAROUND

    const int ChanceFailAttackRightBack = 192;
    DropItem "Chaingun", 0, 0
    DamageFactor DRRPPlasmaDmg, 2.0

    States {
    Missile:
        CPOS E 10 A_FaceTarget
        CPOS FE 4 Bright A_CustomBulletAttack( 22.5, 0, 1, Random( 1, 5 ) * 3, "BulletPuff", 0, CBAF_NORANDOM )
        CPOS F 1 A_CPosRefire
        Goto Missile + 1
    Pain:
        CPOS G 3 A_Pain
        CPOS G 0 A_JumpIfCloser( 196, 1 )
        Goto PainOut
        CPOS G 2 A_Jump( ChanceFailAttackRightBack, "PainOut" )
        Goto Missile + 1
    PainOut:
        CPOS G 1
        Goto See
    }
}

Actor Troop : ChaingunGuyDRRP 10264 {
    //$Category DMonsters
    //$Sprite CPOSA1

    Obituary "$DRRP_OB_TROOP"
    Tag "$DRRP_M_TROOP"
    Health 55
    Translation "32:47=163:167"
}

Actor Commando : ChaingunGuyDRRP 10364 {
    //$Category DMonsters
    //$Sprite CPOSA1
    const int ChanceFailAttackRightBack = 184;

    Obituary "$DRRP_OB_COMMANDO"
    Tag "$DRRP_M_COMMANDO"
    Health 130
    Translation "16:47=192:207"

    States {
    Missile:
        CPOS E 10 A_FaceTarget
        CPOS FE 4 Bright A_CustomBulletAttack( 22.5, 0, 1, Random( 1, 5 ) * 3 + 2, "BulletPuff", 0, CBAF_NORANDOM )
        CPOS F 1 A_CPosRefire
        Goto Missile + 1
    }
}

Actor Assassin : ChaingunGuyDRRP replaces ChaingunGuy 10464 {
    //$Category DMonsters
    //$Sprite CPOSA1
    const int ChanceFailAttackRightBack = 160;

    Obituary "$DRRP_OB_ASSASIN"
    Tag "$DRRP_M_ASSASSIN"
    Health 260

    States {
    Missile:
        CPOS E 10 A_FaceTarget
        CPOS FE 4 Bright A_CustomBulletAttack( 22.5, 0, 1, Random( 2, 6 ) * 3 + 2, "BulletPuff", 0, CBAF_NORANDOM )
        CPOS F 1 A_CPosRefire
        Goto Missile + 1
    }
}


// Imps -------------------------------------------------------------------------------

Actor Impling : DoomImp 10260 {
    //$Category DMonsters
    //$Sprite TROOA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_IMPLING"
    HitObituary "$DRRP_OBM_IMPLING"
    Tag "$DRRP_M_IMPLING"
    Health 50
    Translation "64:79=112:127", "61:63=112:118"
}

Actor DRRPImp : DoomImp replaces DoomImp 10360 {
    //$Category DMonsters
    //$Sprite TROOA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_IMP"
    HitObituary "$DRRP_OBM_IMPLING"
    Tag "$DRRP_M_IMP"
    Health 115
}

Actor ImpLord : DoomImp 10460 {
    //$Category DMonsters
    //$Sprite TROOA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_IMPLORD"
    HitObituary "$DRRP_OBM_IMPLORD"
    Tag "$DRRP_M_IMPLORD"
    Health 290
    Translation "64:79=174:191", "175:191=112:127"
}

Actor DRRPDoomImpBall: DoomImpBall replaces DoomImpBall {
    Damage (Random( 1, 3 ) * 6 + 3)
}


// Lost souls -------------------------------------------------------------------------

Actor DRRPLostSoul : LostSoul replaces LostSoul 10363 {
    //$Category DMonsters
    //$Sprite SKULA1

    +LOOKALLAROUND
    -DONTFALL

    Obituary "$DRRP_OB_LOSTSOUL"
    Tag "$DRRP_M_LOSTSOUL"
    Health 150
    Damage (Random( 1, 3 ) * 8 + 3)
    DamageFactor DRRPFireExtDmg, 5.0
    DamageFactor DRRPFireDmg, 0.0

    States {
        See:
            SKUL AABB 3 A_Chase
            Loop
        Death:
            SKUL F 6 Bright
            SKUL G 6 Bright A_Scream
            SKUL H 6 Bright
            SKUL I 6 Bright A_NoBlocking
            SKUL J 6
            SKUL K 6
            SOUL Z -1
            Stop
        Death.DRRPFireExtDmg:
            LSOL F 6 Bright A_Gravity
            LSOL G 5 Bright A_Scream
            LSOL H 5 Bright A_NoBlocking
            LSOL I 5 Bright
            SOUL Z -1
            Stop    
    }
}

Actor Phantom : DRRPLostSoul 10263 {
    //$Category DMonsters
    //$Sprite SKULA1

    Obituary "$DRRP_OB_PHANTOM"
    Tag "$DRRP_M_PHANTOM"
    Health 40
    Damage (Random( 2, 3 ) * 8 + 5)
    Translation "128:151=152:159", "64:79=152:159"
}

Actor Nightmare : DRRPLostSoul 10463 {
    //$Category DMonsters
    //$Sprite SKULA1

    Obituary "$DRRP_OB_NIGHTMARE"
    Tag "$DRRP_M_NIGHTMARE"
    Health 350
    Damage (Random( 1, 3 ) * 8 + 15)
    Translation "128:151=195:207", "64:79=200:207"
}


// Demons -----------------------------------------------------------------------------

Actor BullDemon : Demon 10259 {
    //$Category DMonsters
    //$Sprite SARGA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_BULLDEMON"
    Tag "$DRRP_M_BULLDEMON"
    Health 85
    Translation "16:47=128:143", "168:191=144:151"

    States {
    Melee:
        SARG EF 8 Fast A_FaceTarget
        SARG G 8 Fast A_CustomMeleeAttack( 4 * Random( 1, 7 ) )
        Goto See
    }
}

Actor Pinky : Demon replaces Demon 10459 { //FIXME: id
    //$Category DMonsters
    //$Sprite SARGA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_PINKY"
    Tag "$DRRP_M_PINKY"
    Health 250

    States {
    Melee:
        SARG EF 8 Fast A_FaceTarget
        SARG G 8 Fast A_CustomMeleeAttack( 4 * Random( 1, 10 ) )
        Goto See
    }
}

Actor Belphegor : Demon 10359 {
    //$Category DMonsters
    //$Sprite SARGA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_BEPHELGOR"
    Tag "$DRRP_M_BELPHEGOR"
    Health 400
    Translation "16:47=%[0.00,0.00,0.00]:[0.65,0.89,1.61]"

    States {
    Melee:
        SARG EF 8 Fast A_FaceTarget
        SARG G 8 Fast A_CustomMeleeAttack( 4 * Random( 4, 10 ) + 7 )
        Goto See
    }
}


// Cacodemons -------------------------------------------------------------------------

Actor Malwrath : Cacodemon 10262 {
    //$Category DMonsters
    //$Sprite HEADA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_MALWRATH"
    Tag "$DRRP_M_MALWRATH"
    Health 195
    Translation "168:191=160:167", "16:47=160:166", "112:127=176:191", "192:207=176:191", "240:247=191:191" //"173:191=[215, 222, 0]:[107, 65, 0]", "16:47=[214, 190, 123]:[107, 81, 0]", "167:167=164:164", "192:207=32:39", "240:247=40:47", "112:127=16:47"
    DamageFactor DRRPAxeDmg, 3.0

    States {
    Missile:
        HEAD BC 5 A_FaceTarget
        HEAD D 5 Bright A_CustomComboAttack( "CacodemonBall", 32, 10 * Random( 1, 3 ) )
        Goto See
    }
}

Actor DRRPCacodemon : Cacodemon replaces Cacodemon 10362 {
    //$Category DMonsters
    //$Sprite HEADA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_CACODEMON"
    Tag "$DRRP_M_CACODEMON"
    Health 365
    DamageFactor DRRPAxeDmg, 3.0

    States {
    Missile:
        HEAD BC 5 A_FaceTarget
        HEAD D 5 Bright A_CustomComboAttack( "CacodemonBall", 32, 10 * Random( 1, 5 ) + 5 )
        Goto See
    }
}

Actor Wretched : Cacodemon 10462 {
    //$Category DMonsters
    //$Sprite HEADA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_WRETCHED"
    Tag "$DRRP_M_WRETCHED"
    Health 650
    Translation "168:191=193:207", "16:47=193:201", "192:207=168:191", "223:223=207:207"
    DamageFactor DRRPAxeDmg, 3.0

    States {
    Missile:
        HEAD BC 5 A_FaceTarget
        HEAD D 5 Bright A_CustomComboAttack( "CacodemonBall", 32, 10 * Random( 2, 8 ) )
        Goto See
    }
}

Actor DRRPCacodemonBall: CacodemonBall replaces CacodemonBall {
    Damage (5 * Random( 1, 8 ))
}


// Revenants --------------------------------------------------------------------------

Actor DRRPRevenant : Revenant replaces Revenant 10477 { // Huesos (Spanish)
    //$Category DMonsters
    //$Sprite SKELA1D1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_REVENANT"
    HitObituary "$DRRP_OBM_REVENANT"
    Tag "$DRRP_M_REVENANT"
    Health 630
    Scale 0.74

    States {
    Melee:
        SKEL G 0 A_FaceTarget
        SKEL G 6 A_SkelWhoosh
        SKEL H 6 A_FaceTarget
        SKEL I 6 A_CustomMeleeAttack( Random( 1, 8 ) * 6, "skeleton/melee" )
        Goto See
    Missile:
        SKEL J 0 Bright A_FaceTarget
        SKEL J 10 Bright A_FaceTarget
        SKEL K 10 A_SpawnProjectile( "DRRPRevenantTracer" )
        SKEL K 10 A_FaceTarget
        Goto See
    Death:
        SKEL L 7 A_SpawnItem("RocketAmmo")
        SKEL M 7 A_SpawnItem("DRRPExplosion", 0, 38)
        SKEL M 0 A_Jump( 256, "DeathReal" ) // For final monster cast.
        SKEL N 7 A_Scream
        SKEL OP 7
        SKEL Q -1
        Stop
    DeathReal:
        TNT1 A 7 A_Scream
        TNT1 A 7 A_NoBlocking
        TNT1 A 7
        TNT1 A -1
        Stop
    }
}

Actor Ghoul : DRRPRevenant 10277 {
    //$Category DMonsters
    //$Sprite SKELA1D1

    Obituary "$DRRP_OB_GHOUL"
    HitObituary "$DRRP_OBM_GHOUL"
    Tag "$DRRP_M_GHOUL"
    Health 130
    Translation "128:143=112:127", "144:151=120:127", "48:79=112:127", "160:167=114:126", "13:15=126:127", "232:235=124:124", "20:20=116:116", "236:239=125:127", "21:47=160:16"

    States {
    Melee:
        SKEL G 0 A_FaceTarget
        SKEL G 6 A_SkelWhoosh
        SKEL H 6 A_FaceTarget
        SKEL I 6 A_CustomMeleeAttack( Random( 4, 10 ) * 6, "skeleton/melee" )
        Goto See
    }
}

Actor Fiend : DRRPRevenant 10377 {
    //$Category DMonsters
    //$Sprite SKELA1D1

    Obituary "$DRRP_OB_FIEND"
    HitObituary "$DRRP_OBM_FIEND"
    Tag "$DRRP_M_FIEND"
    Health 310
    Translation "128:159=194:207", "48:79=196:207", "160:167=240:240", "236:239=243:243", "170:170=240:240", "16:47=252:254", "20:20=207:207", "184:184=242:242", "9:9=207:207", "144:151=195:204", "13:15=241:241"
    States {
    Melee:
        SKEL G 0 A_FaceTarget
        SKEL G 6 A_SkelWhoosh
        SKEL H 6 A_FaceTarget
        SKEL I 6 A_CustomMeleeAttack( Random( 3, 15 ) * 6, "skeleton/melee" )
        Goto See
    }
}

Actor DRRPRevenantTracer: RevenantTracer replaces RevenantTracer {
    Damage (6 * Random( 1, 8 ))
}

// Pain Elementals --------------------------------------------------------------------

Actor DRRPPainElemental : PainElemental replaces PainElemental 10279 {
    //$Category DMonsters
    //$Sprite PAINA1

    +LOOKALLAROUND

    Tag "$DRRP_M_PAINELEMENTAL"
    Health 660
    DamageFactor DRRPFireDmg, 0.0

    States {
        Missile:
            PAIN D 5 A_FaceTarget
            PAIN E 5 A_FaceTarget
            PAIN F 5 Bright A_FaceTarget
            PAIN F 0 Bright A_CustomComboAttack("FatShot", 32, 2 * Random(1, 6))
            Goto See
        Death:
            PAIN H  8 Bright
            PAIN I  8 Bright A_Scream
            PAIN JK 8 Bright
            PAIN L  8 Bright A_PainDie("Nightmare")
            PAIN M  8 Bright
            PAIN Z -1
            Stop
    }
}

Actor Rahovart : DRRPPainElemental 10379 {
    //$Category DMonsters
    //$Sprite PAINA1

    Tag "$DRRP_M_RAHOVART"
    Health 420
    Translation 
"13:13=204:204", "14:14=204:204", "15:15=204:204", "64:64=198:198", "65:65=198:198", "66:66=198:198", "67:67=198:198", "68:68=198:198", "69:69=198:198", "70:70=198:198", "71:71=246:246", "72:72=246:246", "73:73=246:246", "74:74=246:246", "75:75=246:246", "76:76=246:246", "77:77=246:246", "78:78=246:246", "79:79=246:246", "128:128=197:197", "129:129=197:197", "130:130=197:197", "131:131=197:197", "132:132=197:197", "133:133=197:197", "134:134=197:197", "135:135=197:197", "136:136=197:197", "137:137=197:197", "138:138=197:197", "139:139=197:197", "140:140=197:197", "141:142=197:198", "143:143=198:198", "144:144=200:200", "145:151=200:206", "236:236=241:241", "237:237=241:241", "238:238=241:241", "239:239=241:241"


    States {
        Death:
            PAIN H  8 Bright
            PAIN I  8 Bright A_Scream
            PAIN JK 8 Bright
            PAIN L  8 Bright A_PainDie("DRRPLostSoul")
            PAIN M  8 Bright
            PAIN Z -1
            Stop
    }
}

Actor Beholder : DRRPPainElemental 10479 {
    //$Category DMonsters
    //$Sprite PAINA1

    Tag "$DRRP_M_BEHOLDER"
    Health 230
    Translation "128:143=%[0.00,0.00,0.00]:[0.92,1.18,0.66]", "13:15=9:10", "64:79=%[0.00,0.00,0.00]:[0.97,1.20,0.62]", "13:15=%[0.00,0.00,0.00]:[0.80,1.02,0.51]", "236:239=%[0.00,0.00,0.00]:[0.66,0.83,0.49]", "1:2=12:12", "167:167=12:12", "165:165=159:159"
    States {
        Death:
            PAIN H  8 Bright
            PAIN I  8 Bright A_Scream
            PAIN JK 8 Bright
            PAIN L  8 Bright A_PainDie("Phantom")
            PAIN M  8 Bright
            PAIN Z -1
            Stop            
    }
}


// Mancubuses -------------------------------------------------------------------------
// Dull joke by UsernameAK: "Minecraft -- cube world".
// - PROPHESSOR laughs

Actor DRRPCubus : Fatso {
    +LOOKALLAROUND

    Scale 0.92
    Radius 30
    Height 52
    DamageFactor DRRPRocketDmg, 2.0

    States {
    Missile:
        FATT G 20 A_FatRaise
        FATT H 10 Bright A_FatAttack1( "DRRPFatShot" )
        FATT IG 5 A_FaceTarget
        FATT H 10 Bright A_FatAttack2( "DRRPFatShot" )
        FATT IG 5 A_FaceTarget
        FATT H 10 Bright A_FatAttack3( "DRRPFatShot" )
        FATT IG 5 A_FaceTarget
        Goto See
    }
}

Actor Behemoth : DRRPCubus 10278 {
    //$Category DMonsters
    //$Sprite FATTA1

    Obituary "$DRRP_OB_BEHEMOTH"
    Tag "$DRRP_M_BEHEMOTH"
    Health 320
    Translation "48:79=%[0.00,0.00,0.00]:[0.95,0.89,1.83]", "128:143=%[0.00,0.00,0.00]:[1.02,1.04,1.57]", "112:127=176:191", "160:167=%[0.00,0.00,0.00]:[1.02,0.94,1.91]", "212:223=%[0.00,0.00,0.00]:[0.90,0.82,1.84]", "13:15=%[0.00,0.00,0.00]:[0.83,0.83,2.00]", "236:239=%[0.00,0.00,0.00]:[0.83,0.83,2.00]", "144:151=%[0.00,0.00,0.00]:[0.83,0.83,2.00]", "176:191=%[0.00,0.00,0.00]:[0.83,0.83,2.00]", "16:47=%[0.00,0.00,0.00]:[2.00,0.00,0.00]"
}

Actor Mancubus : DRRPCubus replaces Fatso 10378 {
    //$Category DMonsters
    //$Sprite FATTA1

    Obituary "$DRRP_OB_MANCUBUS"
    Tag "$DRRP_M_MANCUBUS"
    Health 700
}

Actor Druj : DRRPCubus 10478 {
    //$Category DMonsters
    //$Sprite FATTA1

    Tag "$DRRP_M_DRUJ"
    Obituary "$DRRP_OB_DRUJ"
    Health 1024
    Translation "48:79=16:45", "144:151=16:45", "144:151=16:45", "236:239=46:46", "13:15=46:46", "141:141=46:46", "138:138=46:46", "142:142=45:45", "143:143=45:45", "16:47=176:189"
}

Actor DRRPFatShot: FatShot replaces FatShot {
    Damage (8 * Random( 1, 8 ))
}

// Arch-Viles -------------------------------------------------------------------------

Actor DRRPVile : ArchVile {
    +LOOKALLAROUND

    Scale 0.7
    const int vileInitialDamage = 15;
    const int vileBlastDamageNRadius = 50;
    DamageFactor DRRPAxeDmg, 5.0
    DamageFactor DRRPFireDmg, 0.0
    
    States {
    
    See:
        TNT1 A 0 Bright A_JumpIf(CheckClass("DRRPFire", AAPTR_TARGET, true), "Spawn") // Reset target if it's fire
        VILE AABBCCDDEEFF 2 A_VileChase
        Loop
    Missile:
        VILE G 0 Bright A_VileStart
        VILE G 10 Bright A_FaceTarget
        VILE H 8 Bright A_VileTarget
        VILE IJKLMN 8 Bright A_FaceTarget
        VILE O 8 Bright A_VileAttack( "vile/stop", vileInitialDamage, vileBlastDamageNRadius, vileBlastDamageNRadius, 0.2 )
        VILE P 20 Bright
        Goto See
    }
}

Actor Infernis : DRRPVile 10273 {
    //$Category DMonsters
    //$Sprite VILEA1D1

    Obituary "$DRRP_OB_INFERNIS"
    Tag "$DRRP_M_INFERNIS"
    Health 180
    Translation  "48:79=192:207", "128:143=192:207", "13:15=240:247", "144:151=240:247", "236:239=240:247", "144:151=240:247", "208:223=192:207", "0:2=247:247"
    const int vileInitialDamage = 12;
    const int vileBlastDamageNRadius = 40;
}

Actor DRRPArchVile : DRRPVile replaces ArchVile 10373 {
    //$Category DMonsters
    //$Sprite VILEA1D1

    Obituary "$DRRP_OB_ARCHVILE"
    Tag "$DRRP_M_ARCHVILE"
    Health 385
}

Actor Apollyon : DRRPVile 10473 {
    //$Category DMonsters
    //$Sprite VILEA1D1

    Obituary "$DRRP_OB_APOLLYON"
    Tag "$DRRP_M_APOLLYON"
    Health 700
    Translation "64:79=32:47", "48:63=16:31", "128:143=16:39", "236:239=44:47", "13:15=45:47", "144:151=32:39" // "16:223=%[0.00, 0.00, 0.00]:[2.00, 0.78, 0.78]"
    const int vileInitialDamage = 25;
    const int vileBlastDamageNRadius = 75;
}


// Barons of Hell ---------------------------------------------------------------------

Actor DRRPBaronOfHell : BaronOfHell replaces BaronOfHell 10280 {
    //$Category DMonsters
    //$Sprite BOSSA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_BARON"
    HitObituary "$DRRP_OBM_BARON"
    Tag "$DRRP_M_BARON"
    Scale 0.85
    Health 1200
    DamageFactor DRRPSSGDmg, 5.0

    States {
    Melee:
    Missile:
        BOSS EF 8 A_FaceTarget
        BOSS G 8 A_CustomComboAttack( "DRRPBaronBall", 32, 10 * Random( 4, 9 ), "baron/melee" )
        Goto See
    }
}

Actor DRRPHellKnight : HellKnight replaces HellKnight 10380 {
    //$Category DMonsters
    //$Sprite BOS2A1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_HELLKNIGHT"
    HitObituary "$DRRP_OBM_HELLKNIGHT"
    Tag "$DRRP_M_HELLKNIGHT"
    Scale 0.85
    Health 550
    DamageFactor DRRPSSGDmg, 5.0

    States {
    Melee:
    Missile:
        BOS2 EF 8 A_FaceTarget
        BOS2 G 8 A_CustomComboAttack( "DRRPBaronBall", 32, 9 * Random( 1, 8 ) + 6, "baron/melee" )
        Goto See
    }
}

Actor Ogre : DRRPHellKnight 10480 {
    //$Category DMonsters
    //$Sprite BOS2A1

    Obituary "$DRRP_OB_OGRE"
    HitObituary "$DRRP_OBM_OGRE"
    Tag "$DRRP_M_OGRE"
    Health 290
    Translation "112:127=176:191", "128:143=%[0.00,0.00,0.00]:[0.77,0.82,0.64]", "144:151=%[0.00,0.00,0.00]:[0.80,0.85,0.65]", "48:63=%[0.00,0.00,0.00]:[0.76,0.82,0.64]", "13:15=%[0.00,0.00,0.00]:[0.65,0.69,0.54]"

    States {
    Melee:
    Missile:
        BOS2 EF 8 A_FaceTarget
        BOS2 G 8 A_CustomComboAttack( "DRRPBaronBall", 32, 7 * Random( 2, 7 ) + 4, "baron/melee" )
        Goto See
    }
}

Actor DRRPBaronBall: BaronBall replaces BaronBall {
    Damage (8 * Random( 1, 8 ))
}


// TODO:
Actor DRRPCyberdemon : Cyberdemon replaces Cyberdemon 10390 {
    //$Category DMonsters
    //$Sprite CYBRA1

    +LOOKALLAROUND

    Obituary "$DRRP_OB_CYBERDEMON"
    Tag "$DRRP_M_CYBERDEMON"
    Health 6666
}

Actor Kronos 10391 {
    //$Category DMonsters
    //$Sprite KRONA1

    MONSTER
    +LOOKALLAROUND
    +BOSS
    +NOGRAVITY
    +FLOAT
    Radius   24
    Height   56
    Health   3666 // Original: 344..572
    Speed    20
    Scale    0.43
    Obituary "$DRRP_OB_KRONOS"
    Tag      "$DRRP_M_KRONOS"
    //SeeSound "DRRPMonsters/Kronos/sight"
    //PainSound "DRRPMonsters/Kronos/pain"
    //DeathSound "DRRPMonsters/Kronos/death"
    //ActiveSound "DRRPMonsters/Kronos/active"
    //AttackSound "DRRPMonsters/Kronos/attack"
    SeeSound "demon/sight"
    PainSound "skull/pain"
    ActiveSound "demon/active"
    States {
        Spawn:
            KRON A 2 A_Look
            Wait
        See:
            KRON AAA 2 A_Chase
            KRON A 0 A_SetScale( -0.43, 0.43 )
            KRON AAA 2 A_Chase
            KRON A 0 A_SetScale( 0.43, 0.43 )
            Loop
        Melee:
            KRON A 2
            KRON A 5 A_CustomMeleeAttack( Random( 1, 10 ), "imp/melee", "skeleton/whoosh" )
            KRON A 3
            Goto See
        Death:
            KRON M 3 {
                A_Fall;
                A_NoBlocking;
                A_PlaySound( "demon/death", CHAN_AUTO );
                A_PlaySound( "cyber/death", CHAN_AUTO );
            }
            KRON NOP 2
            KRON QRST 3
            KRON UVWX 2
            KRON Y 15
            KRON Z -1
            Stop
    }    
}
