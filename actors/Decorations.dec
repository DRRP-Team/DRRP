/**
 * Copyright (c) 2017-2021 DRRP-Team
 *
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

//10701-10800

Actor DRRPBarrel : ExplosiveBarrel replaces ExplosiveBarrel  10701 {
    //$Category DDecorations

    Tag "$DRRP_E_BARREL"
    Health 1
    Radius 32
    BloodColor "00 FF 00"
    DamageFactor DRRPFireExtDmg, 0.0

    DeathSound "generic/expl/barrel"

    States  {
        Spawn:
            BAR1 AB 6
            Loop
        Death:
            BEXP A 5 Bright
            BEXP B 5 Bright A_Scream
            BEXP C 0 Bright A_Quake(6, 5, 0, 128)
            BEXP C 5 Bright
            BEXP D 5 Bright A_Explode
            BEXP E 10 Bright
            TNT1 A 1050 Bright A_BarrelDestroy
            Stop
    }
}

Actor DRRPTable 10702 {
    //$Category DDecorations

    +SOLID
    +NOBLOODDECALS
    -SHOOTABLE

    Tag "$DRRP_E_TABLE"
    Radius 20
    Height 27
    Mass 0x7FFFFFFF
    ProjectilePassHeight -16

    States {
        Spawn:
            DRTB A -1
            Stop
    }
}

Actor DRRPYellowLamp : Column replaces Column {
    //$Category DDecorations

    Tag "$DRRP_E_YELLOWLAMP"
    Radius 20

    States {
        Spawn:
            TNT1 A 0
            TNT1 A 0 A_SpawnItemEx( "DRRPLight1", 0, 0, 40 )
            Goto Super::Spawn
    }
}

Actor DRRPBlueLamp : DRRPYellowLamp 10703 {
    //$Category DDecorations

    Tag "$DRRP_E_BLUELAMP"
    Translation "224:231=192:195", "160:167=196:199"
}

Actor DRRPLight1 10713 {
    //$Category DDecorations

    -SOLID
    +NOGRAVITY

    Alpha 0.12
    Renderstyle Add
    Height 0    // For skip infobar tag handling.
    Radius 0

    States {
        Spawn:
            GLOW A -1 Bright
            Stop
    }
}

Actor DRRPLight2: DRRPLight1 10714 {
    //$Category DDecorations
    +SPAWNCEILING

    States {
        Spawn:
            GLOW B -1 Bright
            Stop
    }
}

Actor DRRPLight3: DRRPLight1 10715 {
    //$Category DDecorations

    States {
        Spawn:
            GLOW C -1 Bright
            Stop
    }
}

Actor DRRPChair 10750 {
    //$Category DDecorations

    +SOLID
    +NOTAUTOAIMED
    -SHOOTABLE
    Tag "$DRRP_E_CHAIR"
    Radius 14
    Height 37
    Mass 0x7FFFFFFF
    States {
        Spawn:
            CHAI A -1
            Stop
    }
}

Actor DRRPRod : ExplosiveBarrel 10704 {
    //$Category DDecorations

    Tag "$DRRP_E_POWERCOUPLING"
    Scale 0.6
    Health 666
    Mass 10000000
    DamageFactor DRRPFireExtDmg, 0.0
    States {
        Spawn:
            ROD1 ABCDEF 2
            Loop
        Death:
            ROD2 AB 1
            ROD2 C  1  Bright A_SpawnItem("DRRPExplosion", 0, 38)
            ROD2 DEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
            ROD3 A  1  Bright
            ROD3 B  1  Bright A_Quake(6, 10, 0, 256, "generic/expl/barrel")
            ROD3 C  2  Bright A_Scream
            ROD3 D  1  Bright
            ROD3 E  1  Bright A_Quake(6, 10, 0, 256, "generic/expl/barrel")
            ROD3 F  5  Bright A_Explode
            ROD3 D  10 Bright
            ROD3 G  1  Bright
            ROD3 H  1  Bright A_Quake(6, 10, 0, 256)
            ROD3 IJKLM 1
            ROD3 N  1 ACS_ExecuteAlways(101, 0)
            ROD3 N  1 A_SpawnItem("DRRPRodTrash")
            Stop
    }
}

Actor DRRPRodTrash {
    +SOLID
    +SHOOTABLE

    Tag "$DRRP_E_POWERCOUPLING"
    Scale 0.6
    Health 10
    Mass 10000000
    DamageFactor DRRPFireExtDmg, 0.0

    States {
        Spawn:
            ROD3 N 5
            Loop
        Death:
            ROD3 N 5 Bright A_SpawnItem("DRRPExplosion", 0, 20)
            TNT1 A 0 A_ChangeFlag("SOLID", FALSE)
            Stop
    }
}

Actor DRRPCrate 10705 {
    //$Category DDecorations

    +SOLID
    +SHOOTABLE
    +NOBLOOD
    +NOBLOODDECALS
    +FLOORCLIP
    +INCOMBAT

    Tag "$DRRP_E_CRATE"
    Health 15 // 20
    Height 32
    Radius 25
    PainChance 255
    Mass 0x7FFFFFFF
    DamageFactor "DRRPFireExtDmg", 0
    //BloodColor "AA AA AA"
    States {
        Spawn:
            CRAT A -1
            Stop
        Pain:
            CRAT A 0
            CRAT AA 0 A_SpawnItemEx("DRRPCrateShard", FRandom(-5, 5), FRandom(-5, 5), Random(3, 7), Random(-5, 5), Random(-5, 5), Random(1, 5))
            CRAT A 0 A_PlaySound("generic/crate/hit")
            Goto Spawn
        Death:
            CRAT A 0
            CRAT AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DRRPCrateShard", FRandom(-5, 5), FRandom(-5, 5), Random(1, 6), Random(-4, 4), Random(-4, 4), Random(1, 4))
            CRAT A 0 A_Quake(1, 18, 0, 128)
            CRT2 B 4 A_PlaySound("generic/crate/hit", CHAN_ITEM)
            CRT2 C 3 //A_SpawnItemEx("DRRPCrateSpawner") // See "/ZScript/RPGSystem.zsc": "WorldThingDamaged()" calls "deathDropGetName()".
            CRT2 D 3
            CRT2 E 3 A_NoBlocking
            CRT2 FG 4
            CRT2 H -1
            Stop
    }
}

Actor DRRPYellowCrate : DRRPCrate 10766 {}

Actor DRRPCrateShard {
    -SOLID
    -NOGRAVITY
    -BLOCKEDBYSOLIDACTORS
    //+NOCLIP
    +NOBLOCKMAP
    +BOUNCEONFLOORS
    +BOUNCEONWALLS
    Radius 2
    Height 3
    BounceFactor 0.3
    States {
    Spawn:
        BPRT A 0
        BPRT A 0 A_SetScale(FRandomPick(-0.75, -0.5, 0.5, 0.75), FRandomPick(-0.75, -0.5, 0.5, 0.75))
        BPRT A 0 A_Jump(255, "Type1", "Type2", "Type3", "Type4", "Type5", "Type6")
    Type1:
        BPRT ABCDEFGHABCDEFGH 2
        Goto Death
    Type2:
        BPRT ABCDEFGHABCD 2
        Goto Death
    Type3:
        BPRT ABCDE 3
        Goto Death
    Type4:
        BPRT ABCDEFG 3
        Goto Death
    Type5:
        BPRT ABC 5
        Goto Death
    Type6:
        BPRT ABCDEFGHABCDEF 2
        Goto Death
    Death:
        "####" "#" 300
        "####" "#" 2 A_FadeOut(0.02)
        Wait
    }
}

Actor DRRPCrateSpawner: RandomSpawner {
    // See "/ZScript/RPGSystem.zsc": "WorldThingDamaged()" calls "deathDropGetName()".
    DropItem "ArmorBonus"
    DropItem "SmallMedikit"
    DropItem "Credit"
    DropItem "GoldCredit"
    DropItem "Shell"
    DropItem "Cell"
    DropItem "Clip"
    DropItem "Balloon"
    DropItem "RocketAmmo"
    DropItem "DRRPExplosion"
}

Actor DRRPWardrobe 10706  {
    //$Category DDecorations

    +SOLID
    +NOBLOODDECALS
    -SHOOTABLE

    Tag "$DRRP_E_CABINET"
    Radius 20
    Height 50
    Mass 0x7FFFFFFF
    ProjectilePassHeight -16

    States {
        Spawn:
            DCAB A -1
            Stop
    }
}

Actor DRRPBed 10707  {
    //$Category DDecorations

    +SOLID

    Tag "$DRRP_E_BUNKBEDS"
    Radius 32
    Height 57
    Mass 0x7FFFFFFF

    States {
        Spawn:
            BEDS A -1
            Stop
    }
}

Actor DRRPBed_WithoutSprite: DRRPBed 10711 {
    //$Category DDecorations

    States {
        Spawn:
            TNT1 A -1
            Stop
    }
}

Actor DRRPEnter 10712  {
    //$Category DDecorations

    -SOLID

    Tag "$DRRP_E_ENTERDOOR"
    Radius 14
    Height 64
    Mass 0x7FFFFFFF

    States {
        Spawn:
            ENTR AB 35 Bright
            Loop
    }
}

Actor DRRPExit 10708  {
    //$Category DDecorations

    -SOLID

    Tag "$DRRP_E_EXITDOOR"
    Radius 14
    Height 64
    Mass 0x7FFFFFFF

    States {
        Spawn:
            EXIT AB 35 Bright
            Loop
    }
}

Actor DRRPSink 10709  {
    //$Category DDecorations

    +SOLID
    +SHOOTABLE

    Tag "$DRRP_E_SINK"
    Radius 14
    Height 64
    Health 50
    Scale 0.8
    Mass 0x7FFFFFFF
    BloodColor "AA AA AA"

    States {
        Spawn:
            XSIN A -1
            Stop
        Death:
            TNT1 A 0 A_PlaySoundEx("Toilet/Crash", CHAN_VOICE)
            TNT1 A 0 A_SpawnItem("DRRPAqueductFountain")
            Stop
    }
}

Actor DRRPToilet 10710 {
    //$Category DDecorations

    +SOLID
    +SHOOTABLE

    Tag "$DRRP_E_TOILET"
    Radius 14
    Height 64
    Health 50
    Scale 0.8
    Mass 0x7FFFFFFF
    BloodColor "AA AA AA"

    States {
        Spawn:
            XTOI A -1
            Stop
        Death:
            TNT1 A 0 A_PlaySoundEx("Toilet/Crash", CHAN_VOICE)
            TNT1 A 0 A_SpawnItem("DRRPAqueductFountain")
            Stop
    }
}

Actor DRRPAqueductFountain {
    //$Category DDecorations

    -SOLID

    Tag "DRRP_D_FOUNTAIN"
    Scale 0.7

    States {
        Spawn:
            CEFE A 1
            CEFE A 0 A_PlaySound( "Toilet/Water", CHAN_AUTO, 1, true, 2.33 )
            CEFE AAABBBCCCAAABBBCCCAAA 1 A_SetScale(ScaleX / 100 * 99)
            CEFE BBBAAABBBCCCAAABBBCCC 1 A_SetScale(ScaleX / 1000 * 993)
            CEFE AAABBBCCCAAABBBCCCAAABBBCCC 1 A_SetScale(ScaleX / 1000 * 995)
            CEFE AAABBBCCCAAABBBCCCAAABBBCCCAAABBBCCC 1 A_SetScale(ScaleX / 1000 * 998)
        SpawnLoop:
            CEFE ABC 3
            Loop
    }
}

Actor DRRPCEFEcon 10743  {
    //$Category DDecorations

    -SOLID

    Tag "$DRRP_D_ALARM"
    Scale 0.4

    States {
        Spawn:
            CEFE ABC 5
            Loop
    }
}
Actor DRRPCEFRcon: DRRPCEFEcon 10744 {
    //$Category DDecorations

    Tag "$DRRP_D_ALARM"

    States {
        Spawn:
            CEFR ABC 5
            Loop
    }
}

Actor DRRPAlarm1 10745  {
    //$Category DDecorations

    +NOCLIP
    +NOGRAVITY
    +NOBLOCKMAP
    +SPAWNCEILING
    -SOLID

    Tag "$DRRP_D_ALARM"
    Scale 1.0

    States {
        Spawn:
            ALAR A 0 A_PlaySound("alarm/alarm3", CHAN_5, 0.2, false, ATTN_NONE)
            ALAR ABCDABCDABCDABCDABCD 3
            Loop
    }
}

Actor DRRPAlarm2: KlaxonWarningLight 10746  {
    //$Category DDecorations

    Tag "$DRRP_D_ALARM"
    States {
        Spawn:
            TNT1 A 0
            Goto See
    }
}

Actor DRRPLightningactor777 19689 {
    //$Category DDecorations

    -SOLID

    States {
        Spawn:
            ZOT1 D 0
            ZOT1 D 0 A_PlaySound("weapons/sigilhit", 7)
            ZOT1 ABCDEDCBA 3
            Stop
    }
}

Actor DRRPTunnelLights 19691 {
    //$Category DDecorations

    +NOGRAVITY
    +NOCLIP
    +SPAWNCEILING

    Radius 8
    Height 1
    XScale 0.9
    YScale 0.9
    Alpha 0.1
    Renderstyle Add

    States {
        Spawn:
            LITS A -1 Bright
            Stop
    }
}

Actor DRRPRubbleSmall 19692 {
    //$Category DDecorations

    +FORCEXYBILLBOARD
    -SOLID
    -SHOOTABLE

    Mass 999999
    Scale 1.0
    Radius 24
    Height 16

    States {
        Spawn:
            TNT1 A 0
            TNT1 A 0 A_Jump(127, "Type2")
        Type1:
            RUBL A 1
            RUBL A -1
            Stop
        Type2:
            RUBL B 1
            RUBL B -1
            Stop
    }
}

Actor DRRPRubbleMedium : DRRPRubbleSmall 1332 {
    //$Category DDecorations

    +SOLID
    +SHOOTABLE
    +NOBLOOD

    Radius 32
    Height 32
    Scale 1.0

    States {
        Spawn:
            RUBL C 1
            RUBL C -1
            Stop
    }
}

Actor DRRPRubbleLarge : DRRPRubbleMedium 1333 {
    //$Category DDecorations

    Radius 64
    Height 56
    Scale 1.3

    States {
        Spawn:
            RUBL D 1
            RUBL D -1
            Stop
    }
}

Actor DRRPCeilLamp1 1316 {
    //$Category DDecorations

    +NOGRAVITY
    +NOCLIP
    +SOLID
    +SPAWNCEILING
    Radius 8
    Height 32
    Scale 0.25
    States {
        Spawn:
            ULGT A 1 Bright
            Loop
    }
}

Actor DRRPCeilLamp2: DRRPCeilLamp1 1317  {
    //$Category DDecorations

    States {
        Spawn:
            ULGT D 1 Bright
            Loop
    }
}

Actor DRRPCeilLamp3: DRRPCeilLamp1 1726  {
    //$Category DDecorations

    States {
        Spawn:
            ULGT C 1 Bright
            Loop
    }
}

Actor DRRPShittyFog 10765 {
    //$Category DDecorations
    //$Sprite STEML0

    +NOBLOCKMAP
    +FORCEXYBILLBOARD
    -SOLID

    Scale 0.5
    Alpha 0.1
    RenderStyle Add

    States {
        Spawn:
            FOGI A 10 Bright
            Loop
    }
}
