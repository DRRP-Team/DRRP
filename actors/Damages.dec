/**
 * Copyright (c) 2017-2019 DRRP-Team
 * 
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

Actor DRRPFire : DRRPFire_zspatch 10250 {
    //$Category Damage
    //$Sprite FIREA0

    MONSTER
    +SOLID
    +NOBLOOD
    +NOTARGET
    +NOTRIGGER
    +NOTAUTOAIMED
    -ISMONSTER
    -COUNTKILL

    Tag "$DRRP_E_FIRE"
    Obituary "$DRRP_OB_FIRE"
    Health 10
    Radius 33
    Mass 0x7FFFFFFF
    DamageType DRRPFireDmg
    DamageFactor "DRRPFireExtDmg", 1.0
    DamageFactor "Normal", 0

    States {
        Spawn:
            FIRE A 0 Bright
            FIRE A 0 Bright A_JumpIf(Args[0] != 0, "Spawn.Start")
            FIRE A 0 Bright A_SetArg(0, 255)
        Spawn.Start:
            FIRE A 0 Bright
            FIRE A 0 Bright A_PlaySound("fire/loop3", CHAN_7, (Args[0] * 1.0) / 255, true, 2.5)
            FIRE A 0 Bright A_Jump(256, Random(1, 8))
        Spawn.Loop:
            FIRE ABCDEFGH 2 BRIGHT // See class "DRRPFire_zspatch", Tick() method.
            Loop
        Death:
        XDeath:
            TNT1 A 0
            Stop
    }
}

Actor DRRPLava 10251 {
    //$Category Damage
    //$Sprite LAVAA0

    MONSTER
    +NOBLOOD
    +NOTARGET
    +NOTRIGGER
    +NOTAUTOAIMED
    +SOLID
	+NOCLIP
    -ISMONSTER
    -COUNTKILL
    -SHOOTABLE

    Tag "$DRRP_E_LAVAPOOL"
    Health 10
    Mass 0x7FFFFFFF
    RenderStyle Add
    DamageType DPPRFireDmg    
    DamageFactor "Normal", 0

    States {
        Spawn:
            SPKO D 0 Bright
            SPKO D 0 Bright A_PlaySound("ICOSCRE", CHAN_7, 1, true, 2.5)
            SPKO D 0 Bright A_Jump(256, 1, 2, 3, 4, 5, 6, 7, 8)
        SpawnLoop:
            SPKO ABCDEFGH 2 BRIGHT A_Explode(7, 32, XF_NOTMISSILE)
            Loop
        Pain.FireExtDamage:
            TNT1 A 0
            Goto SpawnLoop
        Death:
        XDeath:
            TNT1 A 0
            Stop
    }
}

// Scripted

Actor Explosion {
    +NOGRAVITY
    +DONTFALL

    Tag "$DRRP_D_EXPLOSION"

    SpawnID 253
    
    States {
        Spawn:
			MISL B 0 Bright
            MISL B 1 Bright A_PlaySound("generic/expl/explode", CHAN_AUTO)
            MISL B 5 Bright A_Explode
            MISL C 5 Bright
            MISL D 4 Bright
            Stop
    }
}
