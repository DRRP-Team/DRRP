/**
 * Copyright (c) 2017-2018 DRRP-Team
 * 
 * This software is released under the MIT License.
 * https://opensource.org/licenses/MIT
 */

//10600-10700
Actor DRRPGreenCard : DoomKey 10622 {
    //$Category Keys

    Tag "$DRRP_I_GREENKEY"
    Inventory.PickupMessage "$DRRP_I_GREENKEY"
    Inventory.Icon          "STKEYS2"
    Inventory.PickupSound   "misc/k_pkup"
    Translation             "168:191=112:127", "16:47=112:127", "1:2=126:127"

    States {
        Spawn:
            GKEY A 10
            GKEY B 10 Bright
            Loop
    }
}

Actor DRRPYellowCard : YellowCard replaces YellowCard {
    Tag "$DRRP_I_YELLOWKEY"
    Inventory.PickupSound "misc/k_pkup"
}

Actor DRRPBlueCard : BlueCard replaces BlueCard {
    Tag "$DRRP_I_BLUEKEY"
    Inventory.PickupSound "misc/k_pkup"
}

Actor DRRPRedCard : RedCard replaces RedCard {
    Tag "$DRRP_I_REDKEY"
    Inventory.PickupSound "misc/k_pkup"
}

Actor SmallMedikit : HealthPickup replaces Medikit 10600 {
    //$Category Items

    +FLOORCLIP
    +INVENTORY.INVBAR

    Tag                     "$DRRP_I_SMMEDKIT"
    Health                  25
    HealthPickup.AutoUse    0
    Inventory.Icon          "MEDIA0"
    Inventory.MaxAmount     99
    Inventory.PickupMessage "Picked up a small medikit"
    Translation             "168:185=229:229", "186:191=163:163"

    States {
        Spawn:
            MEDI A -1
            Stop
    }
}

Actor LargeMedikit : HealthPickup 10601 {
    //$Category Items

    +FLOORCLIP
    +INVENTORY.INVBAR

    Tag                     "$DRRP_I_LGMEDKIT"
    Health                  75
    HealthPickup.AutoUse    0
    Inventory.MaxAmount     99
    Inventory.Icon          "MEDIA0"
    Inventory.PickupMessage "Picked up a large medikit"
    Translation             "168:185=229:229", "80:111=168:191", "3:3=184:184", "186:191=163:163"

    States {
        Spawn:
            MEDI A -1
            Stop
    }
}

Actor HealthPowerUp : UpgradeStamina 10603 {
    Inventory.Amount 10
    Inventory.MaxAmount 70
}

//Ammo 10610-10619
Actor Balloon : Ammo 10610 {
    //$Category DAmmo

    Tag "$DRRP_I_HALONCAN"
    Ammo.BackpackAmount     20
    Ammo.BackpackMaxAmount  600
    Inventory.Amount        20
    Inventory.MaxAmount     300
    Inventory.Icon          "BALLA0"
    Inventory.PickupSound   "weapon/gas"
    Inventory.PickupMessage "Picked up a baloon!"

    States {
        Spawn:
            BALL A -1
            Stop
    }
}

//Coins 10620-10629
Actor Credit : Inventory 10620 {
    //$Category Items

    +DROPPED
    +NOTDMATCH
    +FLOORCLIP
    +INVENTORY.INVBAR
    Tag                     "$DRRP_I_1CREDIT"
    Inventory.Icon          "CREDH0"
    Inventory.PickupMessage "Picked up a credit."
    Inventory.MaxAmount     0x7FFFFFFF
    States {
        Spawn:
            CRED A -1
            Stop
    }
}

Actor GoldCredit : CustomInventory 10621 {
    //$Category Items
    +DROPPED
    +NOTDMATCH
    +FLOORCLIP
    Tag                     "$DRRP_I_5CREDITS"
    Inventory.PickupMessage "Picked up 5 credits."
    // Translation "112:127=%[0.00,0.00,0.00]:[2.00,1.01,0.00]"
    States {
        Spawn:
            CRED B -1
            Stop
        Pickup:
            TNT1 A 0 A_GiveInventory("Credit", 5)
            Stop
    }
}

Actor Berserker : CustomInventory replaces Berserk 10623 {
    //$Category Items

    +COUNTITEM
    +INVENTORY.INVBAR

    Tag "$DRRP_I_BERSERKER"
    Inventory.MaxAmount     99
    Inventory.Icon          "BERSH0"
    Inventory.PickupMessage "$DRRP_I_BERSERKER"
    Inventory.PickupSound   "misc/p_pkup"

    Translation "193:207=171:191", "240:247=185:191"

    States {
        Spawn:
            SOUL ABCDCB 6 Bright
            Loop
        Use:		
            TNT1 A 0 A_GiveInventory("PowerStrength")
            TNT1 A 0 HealThing(100, 0)
            TNT1 A 0 A_SelectWeapon("DRRPAxe") // Fist
            Stop
    }
}

Actor InventorySoulsphere : CustomInventory replaces Soulsphere {
    +COUNTITEM
    +INVENTORY.INVBAR

    Tag "$DRRP_I_SOULSPHERE"
    Inventory.MaxAmount     99
    Inventory.Icon          "SOULH0"
    Inventory.PickupSound   "misc/p_pkup"
    Inventory.PickupMessage "$GOTSUPER"

    States {
        Spawn:
            SOUL ABCDCB 6 Bright
            Loop
        Use:
            TNT1 A 0 A_GiveInventory( "Health", 999 )
            TNT1 A 0 A_GiveInventory( "BlueArmor", 1 )
            Stop
    }
}

Actor DogCollar : CustomInventory 10624 {
    //$Category Items

    +INVENTORY.INVBAR

    Tag                     "$DRRP_I_DOGCOLLAR"
    Inventory.Icon          "DOGCH0"
    Inventory.PickupMessage "Got the dog collar."
    Inventory.MaxAmount     99

    States {
        Spawn:
            DOGC A -1
            Stop
        Use:
            TNT1 A 0
            TNT1 A 0 A_JumpIf( CheckClass( "Cerberus", AAPTR_PLAYER_GETTARGET ), "CerberusLabel" )
            TNT1 A 0 A_JumpIf( CheckClass( "Hellhound", AAPTR_PLAYER_GETTARGET ), "HellhoundLabel" )
            TNT1 A 0 A_JumpIf( CheckClass( "DemonWolf", AAPTR_PLAYER_GETTARGET ), "DemonWolfLabel" )
            Fail
        CerberusLabel:
            TNT1 A 0
            TNT1 A 0 A_Remove( AAPTR_PLAYER_GETTARGET )
            TNT1 A 0 A_GiveInventory( "CerberusGun" )
            TNT1 A 0 A_SelectWeapon( "CerberusGun" )
            Stop
        HellhoundLabel:
            TNT1 A 0
            TNT1 A 0 A_Remove( AAPTR_PLAYER_GETTARGET )
            TNT1 A 0 A_GiveInventory( "HellhoundGun" )
            TNT1 A 0 A_SelectWeapon( "HellhoundGun" )
            Stop
        DemonWolfLabel:
            TNT1 A 0
            TNT1 A 0 A_Remove( AAPTR_PLAYER_GETTARGET )
            TNT1 A 0 A_GiveInventory( "DemonWolfGun" )
            TNT1 A 0 A_SelectWeapon( "DemonWolfGun" )
            Stop
    }
}

Actor DRRPHealthBonus : Health replaces HealthBonus {
    Inventory.Amount 1
    Inventory.PickupMessage "$GOTHTHBONUS" // "Picked up a health bonus."

    States {
        Spawn:
            BON1 ABCDCB 6
            Loop
    }
}
